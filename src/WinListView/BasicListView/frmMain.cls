 
 /*------------------------------------------------------------------------
    File        : frmMain
    Purpose     : Shows how to populate and group an UltraWinListView control with temp-table records.
    Syntax      : 
    Description : 
    Author(s)   : mferrant
    Created     : Thu May 08 11:16:17 EDT 2008
    Notes       : 
  ----------------------------------------------------------------------*/

USING Progress.Lang.*.
USING Progress.Windows.Form.

USING System.Drawing.*.

USING Infragistics.Win.*.
USING Infragistics.Win.UltraWinEditors.*.
USING Infragistics.Win.UltraWinListView.*.

using UltraWinListView.TempTableRecords.*.
using DataServices.ItemService.

CLASS WinListView.BasicListView.frmMain INHERITS Form   : 
	
	DEFINE PRIVATE VARIABLE components AS System.ComponentModel.IContainer.


	DEFINE PRIVATE VARIABLE splitContainer1 AS System.Windows.Forms.SplitContainer NO-UNDO.


	DEFINE PRIVATE VARIABLE ultraListView1 AS Infragistics.Win.UltraWinListView.UltraListView NO-UNDO.
		

	DEFINE PRIVATE VARIABLE ultraButton1 AS Infragistics.Win.Misc.UltraButton NO-UNDO.


	DEFINE PRIVATE VARIABLE groupItems AS Infragistics.Win.UltraWinEditors.UltraCheckEditor NO-UNDO.
	DEFINE PRIVATE VARIABLE ultraGroupBox1 AS Infragistics.Win.Misc.UltraGroupBox NO-UNDO.

    DEFINE PRIVATE VARIABLE itemService AS ItemService NO-UNDO.

    DEFINE PRIVATE VARIABLE ultraGroupBox2 AS Infragistics.Win.Misc.UltraGroupBox NO-UNDO.
    DEFINE PRIVATE VARIABLE ultraLabel1 AS Infragistics.Win.Misc.UltraLabel NO-UNDO.

    DEFINE PRIVATE VARIABLE coView AS Infragistics.Win.UltraWinEditors.UltraComboEditor NO-UNDO.
	CONSTRUCTOR PUBLIC frmMain (  ):
		
		SUPER().
		InitializeComponent().
		populateViewComboBox().
		createListViewColumns().
		populateDataSet().

		coView:VALUE = ultraListView1:VIEW.

	END CONSTRUCTOR.

	DESTRUCTOR PUBLIC frmMain ( ):

        IF VALID-OBJECT(itemService) THEN
            DELETE OBJECT itemService.

		IF VALID-OBJECT(components) THEN DO:
			CAST(components, System.IDisposable):Dispose().
		END.

	END DESTRUCTOR.

	METHOD PRIVATE VOID InitializeComponent ( ):

/* 		NOTE: The following method is generated by the OpenEdge Advanced GUI Visual Designer.

		We strongly suggest that the contents of this method only be modified using the
		Visual Designer to avoid any incompatible modifications.

		Modifying the contents of this method using a code editor will invalidate any support for this file. */

		@VisualDesigner.FormMember (NeedsInitialize="true").
		DEFINE VARIABLE appearance1 AS Infragistics.Win.Appearance NO-UNDO.
		appearance1 = NEW Infragistics.Win.Appearance().
		THIS-OBJECT:splitContainer1 = NEW System.Windows.Forms.SplitContainer().
		THIS-OBJECT:ultraGroupBox2 = NEW Infragistics.Win.Misc.UltraGroupBox().
		THIS-OBJECT:ultraLabel1 = NEW Infragistics.Win.Misc.UltraLabel().
		THIS-OBJECT:coView = NEW Infragistics.Win.UltraWinEditors.UltraComboEditor().
		THIS-OBJECT:ultraGroupBox1 = NEW Infragistics.Win.Misc.UltraGroupBox().
		THIS-OBJECT:ultraButton1 = NEW Infragistics.Win.Misc.UltraButton().
		THIS-OBJECT:groupItems = NEW Infragistics.Win.UltraWinEditors.UltraCheckEditor().
		THIS-OBJECT:ultraListView1 = NEW Infragistics.Win.UltraWinListView.UltraListView().
		THIS-OBJECT:splitContainer1:Panel1:SuspendLayout().
		THIS-OBJECT:splitContainer1:Panel2:SuspendLayout().
		THIS-OBJECT:splitContainer1:SuspendLayout().
		CAST(THIS-OBJECT:ultraGroupBox2, System.ComponentModel.ISupportInitialize):BeginInit().
		THIS-OBJECT:ultraGroupBox2:SuspendLayout().
		CAST(THIS-OBJECT:coView, System.ComponentModel.ISupportInitialize):BeginInit().
		CAST(THIS-OBJECT:ultraGroupBox1, System.ComponentModel.ISupportInitialize):BeginInit().
		THIS-OBJECT:ultraGroupBox1:SuspendLayout().
		CAST(THIS-OBJECT:ultraListView1, System.ComponentModel.ISupportInitialize):BeginInit().
		THIS-OBJECT:SuspendLayout().
		/*  */
		/* splitContainer1 */
		/*  */
		THIS-OBJECT:splitContainer1:Dock = System.Windows.Forms.DockStyle:Fill.
		THIS-OBJECT:splitContainer1:Location = NEW System.Drawing.Point(0, 0).
		THIS-OBJECT:splitContainer1:Name = "splitContainer1".
		/*  */
		/* splitContainer1.Panel1 */
		/*  */
		THIS-OBJECT:splitContainer1:Panel1:Controls:Add(THIS-OBJECT:ultraGroupBox2).
		THIS-OBJECT:splitContainer1:Panel1:Controls:Add(THIS-OBJECT:ultraGroupBox1).
		/*  */
		/* splitContainer1.Panel2 */
		/*  */
		THIS-OBJECT:splitContainer1:Panel2:Controls:Add(THIS-OBJECT:ultraListView1).
		THIS-OBJECT:splitContainer1:Size = NEW System.Drawing.Size(730, 514).
		THIS-OBJECT:splitContainer1:SplitterDistance = 243.
		THIS-OBJECT:splitContainer1:TabIndex = 0.
		/*  */
		/* ultraGroupBox2 */
		/*  */
		THIS-OBJECT:ultraGroupBox2:Controls:Add(THIS-OBJECT:ultraLabel1).
		THIS-OBJECT:ultraGroupBox2:Controls:Add(THIS-OBJECT:coView).
		THIS-OBJECT:ultraGroupBox2:Dock = System.Windows.Forms.DockStyle:Top.
		THIS-OBJECT:ultraGroupBox2:Location = NEW System.Drawing.Point(0, 79).
		THIS-OBJECT:ultraGroupBox2:Name = "ultraGroupBox2".
		THIS-OBJECT:ultraGroupBox2:Size = NEW System.Drawing.Size(243, 110).
		THIS-OBJECT:ultraGroupBox2:TabIndex = 3.
		THIS-OBJECT:ultraGroupBox2:Text = "ListView settings".
		/*  */
		/* ultraLabel1 */
		/*  */
		THIS-OBJECT:ultraLabel1:Location = NEW System.Drawing.Point(6, 23).
		THIS-OBJECT:ultraLabel1:Name = "ultraLabel1".
		THIS-OBJECT:ultraLabel1:Size = NEW System.Drawing.Size(35, 23).
		THIS-OBJECT:ultraLabel1:TabIndex = 1.
		THIS-OBJECT:ultraLabel1:Text = "View:".
		/*  */
		/* coView */
		/*  */
		THIS-OBJECT:coView:Anchor = CAST(Progress.Util.EnumHelper:Or(Progress.Util.EnumHelper:Or(System.Windows.Forms.AnchorStyles:Top, System.Windows.Forms.AnchorStyles:Left), System.Windows.Forms.AnchorStyles:Right), System.Windows.Forms.AnchorStyles).
		THIS-OBJECT:coView:Location = NEW System.Drawing.Point(47, 19).
		THIS-OBJECT:coView:Name = "coView".
		THIS-OBJECT:coView:Size = NEW System.Drawing.Size(187, 21).
		THIS-OBJECT:coView:SyncWithCurrencyManager = FALSE.
		THIS-OBJECT:coView:TabIndex = 0.
		THIS-OBJECT:coView:ValueChanged:SUBSCRIBE(THIS-OBJECT:coView_ValueChanged).
		/*  */
		/* ultraGroupBox1 */
		/*  */
		THIS-OBJECT:ultraGroupBox1:Controls:Add(THIS-OBJECT:ultraButton1).
		THIS-OBJECT:ultraGroupBox1:Controls:Add(THIS-OBJECT:groupItems).
		THIS-OBJECT:ultraGroupBox1:Dock = System.Windows.Forms.DockStyle:Top.
		THIS-OBJECT:ultraGroupBox1:Location = NEW System.Drawing.Point(0, 0).
		THIS-OBJECT:ultraGroupBox1:Name = "ultraGroupBox1".
		THIS-OBJECT:ultraGroupBox1:Size = NEW System.Drawing.Size(243, 79).
		THIS-OBJECT:ultraGroupBox1:TabIndex = 2.
		THIS-OBJECT:ultraGroupBox1:Text = "Populate options".
		/*  */
		/* ultraButton1 */
		/*  */
		THIS-OBJECT:ultraButton1:Anchor = CAST(Progress.Util.EnumHelper:Or(Progress.Util.EnumHelper:Or(Progress.Util.EnumHelper:Or(System.Windows.Forms.AnchorStyles:Top, System.Windows.Forms.AnchorStyles:Bottom), System.Windows.Forms.AnchorStyles:Left), System.Windows.Forms.AnchorStyles:Right), System.Windows.Forms.AnchorStyles).
		THIS-OBJECT:ultraButton1:Location = NEW System.Drawing.Point(3, 45).
		THIS-OBJECT:ultraButton1:Name = "ultraButton1".
		THIS-OBJECT:ultraButton1:Size = NEW System.Drawing.Size(231, 23).
		THIS-OBJECT:ultraButton1:TabIndex = 0.
		THIS-OBJECT:ultraButton1:Text = "Populate UltraWinListView".
		THIS-OBJECT:ultraButton1:Click:SUBSCRIBE(THIS-OBJECT:ultraButton1_Click).
		/*  */
		/* groupItems */
		/*  */
		THIS-OBJECT:groupItems:Anchor = CAST(Progress.Util.EnumHelper:Or(Progress.Util.EnumHelper:Or(Progress.Util.EnumHelper:Or(System.Windows.Forms.AnchorStyles:Top, System.Windows.Forms.AnchorStyles:Bottom), System.Windows.Forms.AnchorStyles:Left), System.Windows.Forms.AnchorStyles:Right), System.Windows.Forms.AnchorStyles).
		appearance1:FontData:BoldAsString = "True".
		THIS-OBJECT:groupItems:Appearance = appearance1.
		THIS-OBJECT:groupItems:Checked = TRUE.
		THIS-OBJECT:groupItems:CheckState = System.Windows.Forms.CheckState:Checked.
		THIS-OBJECT:groupItems:Location = NEW System.Drawing.Point(6, 19).
		THIS-OBJECT:groupItems:Name = "groupItems".
		THIS-OBJECT:groupItems:Size = NEW System.Drawing.Size(231, 20).
		THIS-OBJECT:groupItems:TabIndex = 1.
		THIS-OBJECT:groupItems:Text = "Group Items by Category".
		THIS-OBJECT:groupItems:CheckedChanged:SUBSCRIBE(THIS-OBJECT:groupItems_CheckedChanged).
		/*  */
		/* ultraListView1 */
		/*  */
		THIS-OBJECT:ultraListView1:Anchor = CAST(Progress.Util.EnumHelper:Or(Progress.Util.EnumHelper:Or(Progress.Util.EnumHelper:Or(System.Windows.Forms.AnchorStyles:Top, System.Windows.Forms.AnchorStyles:Bottom), System.Windows.Forms.AnchorStyles:Left), System.Windows.Forms.AnchorStyles:Right), System.Windows.Forms.AnchorStyles).
		THIS-OBJECT:ultraListView1:ItemSettings:AllowEdit = Infragistics.Win.DefaultableBoolean:False.
		THIS-OBJECT:ultraListView1:Location = NEW System.Drawing.Point(3, 12).
		THIS-OBJECT:ultraListView1:Name = "ultraListView1".
		THIS-OBJECT:ultraListView1:Size = NEW System.Drawing.Size(468, 490).
		THIS-OBJECT:ultraListView1:TabIndex = 0.
		THIS-OBJECT:ultraListView1:Text = "ultraListView1".
		/*  */
		/* frmMain */
		/*  */
		THIS-OBJECT:ClientSize = NEW System.Drawing.Size(730, 514).
		THIS-OBJECT:Controls:Add(THIS-OBJECT:splitContainer1).
		THIS-OBJECT:Name = "frmMain".
		THIS-OBJECT:Text = "WinListView".
		THIS-OBJECT:splitContainer1:Panel1:ResumeLayout(FALSE).
		THIS-OBJECT:splitContainer1:Panel2:ResumeLayout(FALSE).
		THIS-OBJECT:splitContainer1:ResumeLayout(FALSE).
		CAST(THIS-OBJECT:ultraGroupBox2, System.ComponentModel.ISupportInitialize):EndInit().
		THIS-OBJECT:ultraGroupBox2:ResumeLayout(FALSE).
		THIS-OBJECT:ultraGroupBox2:PerformLayout().
		CAST(THIS-OBJECT:coView, System.ComponentModel.ISupportInitialize):EndInit().
		CAST(THIS-OBJECT:ultraGroupBox1, System.ComponentModel.ISupportInitialize):EndInit().
		THIS-OBJECT:ultraGroupBox1:ResumeLayout(FALSE).
		CAST(THIS-OBJECT:ultraListView1, System.ComponentModel.ISupportInitialize):EndInit().
		THIS-OBJECT:ResumeLayout(FALSE).

	END METHOD.

	/*------------------------------------------------------------------------------
			Purpose: Creates the ultra list view columns.
			Notes:
	------------------------------------------------------------------------------*/
	METHOD PRIVATE VOID createListViewColumns(  ):
		DEFINE VARIABLE checkEditor    AS UltraCheckEditor           NO-UNDO.
        DEFINE VARIABLE subItemColumn  AS UltraListViewSubItemColumn NO-UNDO.
        DEFINE VARIABLE currencyFormat AS CHARACTER                  NO-UNDO.
		
		subItemColumn = ultraListView1:SubItemColumns:Add("Allocated":U).
		subItemColumn:Text = "Allocated":U.
		subItemColumn:DataType = Progress.Util.TypeHelper:GetType("System.Int64":U).

		/*Add a sub-item column for the unit price...since the value
		  should be expressed as a currency, use the Format property
		  to display it as such.*/
		subItemColumn = ultraListView1:SubItemColumns:Add("UnitPrice").
		subItemColumn:Text = "Price".
		subItemColumn:DataType = Progress.Util.TypeHelper:GetType("System.Decimal").
		currencyFormat = EditorWithMask:CalcDefaultCurrencyMask( System.Globalization.CultureInfo:CurrentCulture, -1, 1, '0' ).
		currencyFormat = REPLACE(currencyFormat, "n", "#" ).
		currencyFormat = SUBSTRING(currencyFormat, 1, LENGTH(currencyFormat) - 1) + "00".
		subItemColumn:Format = currencyFormat.
		subItemColumn:Width = 75.

		/*Add a sub-item column for the units in stock*/
		subItemColumn = ultraListView1:SubItemColumns:Add("UnitsInStock").
		subItemColumn:Text = "Units (On Hand)".
		subItemColumn:DataType = Progress.Util.TypeHelper:GetType("System.Int32").

		/*Add a sub-item column for the units on order*/
		subItemColumn = ultraListView1:SubItemColumns:Add("UnitsOnOrder").
		subItemColumn:Text = "Units (On Order)".
		subItemColumn:DataType = Progress.Util.TypeHelper:GetType("System.Int32").

		/*Add a sub-item column for discontinued...since the field
		  is boolean, we'll call it "available", and flip the value
		  so end users don't see the check and think that it is in stock.*/
		subItemColumn = ultraListView1:SubItemColumns:Add("Available").
		subItemColumn:Text = "Available?".
		subItemColumn:DataType = Progress.Util.TypeHelper:GetType("System.Boolean").

		/*Use a CheckEditor to display the value, and align it middle center*/
		checkEditor = new UltraCheckEditor().
		checkEditor:CheckAlign = ContentAlignment:MiddleCenter.
		subItemColumn:EditorControl = checkEditor.
		
		RETURN.

	END METHOD.

	/*------------------------------------------------------------------------------
			Purpose: Populates the View combo-box with all the valid
			         UltraListViewStyle styles.
			Notes:
	------------------------------------------------------------------------------*/
	METHOD PRIVATE VOID populateViewComboBox(  ):
		DEFINE VARIABLE valueListDetails    AS ValueListItem NO-UNDO.
		DEFINE VARIABLE valueListIcons      AS ValueListItem NO-UNDO.
		DEFINE VARIABLE valueListList       AS ValueListItem NO-UNDO.
		DEFINE VARIABLE valueListTiles      AS ValueListItem NO-UNDO.
		DEFINE VARIABLE valueListThumbnails AS ValueListItem NO-UNDO.
		DEFINE VARIABLE viewItems           AS System.Array  NO-UNDO.

        ASSIGN valueListThumbnails = NEW ValueListItem()
               valueListDetails    = NEW ValueListItem()
               valueListIcons      = NEW ValueListItem()
               valueListList       = NEW ValueListItem()
               valueListTiles      = NEW ValueListItem()
		       valueListDetails:DataValue      = UltraListViewStyle:Details
		       valueListDetails:DisplayText    = "Details":U
		       valueListThumbnails:DataValue   = UltraListViewStyle:Thumbnails
		       valueListThumbnails:DisplayText = "Thumbnails":U
		       valueListIcons:DataValue        = UltraListViewStyle:Icons
		       valueListIcons:DisplayText      = "Icons":U
	           valueListList:DataValue         = UltraListViewStyle:List
		       valueListList:DisplayText       = "List":U
		       valueListTiles:DataValue        = UltraListViewStyle:Tiles
		       valueListTiles:DisplayText      = "Tiles":U.

		viewItems = System.Array:CreateInstance(Progress.Util.TypeHelper:GetType("Infragistics.Win.ValueListItem"), 5).
		viewItems:SetValue(valueListDetails, 0).
		viewItems:SetValue(valueListIcons, 1).
		viewItems:SetValue(valueListList, 2).
		viewItems:SetValue(valueListTiles, 3).
		viewItems:SetValue(valueListThumbnails, 4).
		coView:Items:AddRange(CAST(viewItems, "Infragistics.Win.ValueListItem[]")).

	END METHOD.

	/*------------------------------------------------------------------------------
			Purpose:  																	  
			Notes:  																	  
	------------------------------------------------------------------------------*/
	
	METHOD PRIVATE VOID populateDataSet(  ):
        itemService = NEW ItemService().
        itemService:fetchData().       
	END METHOD.

	/*------------------------------------------------------------------------------
			Purpose: Creates the list of itmes in the UltraListView.
			Notes:
	------------------------------------------------------------------------------*/
	METHOD PRIVATE VOID populateListViewWithGroups(  ):
	    DEFINE VARIABLE record       AS System.Array       NO-UNDO.
	    DEFINE VARIABLE queryHandle  AS HANDLE             NO-UNDO.
	    DEFINE VARIABLE oldCategory  AS CHARACTER          NO-UNDO.
	    DEFINE VARIABLE category     AS UltraListViewGroup NO-UNDO.
	    DEFINE VARIABLE currentItem  AS UltraListViewItem  NO-UNDO.
	    DEFINE VARIABLE defaultImage AS Bitmap             NO-UNDO.
        DEFINE VARIABLE itemBuffer   AS HANDLE             NO-UNDO. 
        ultraListView1:Groups:Clear().
		ultraListView1:Items:Clear().
        /*Each record in the UltraListView is represented by an array, each element of the array is a
          record field, and is associated to a column.*/
        record = System.Array:CreateInstance(Progress.Util.TypeHelper:GetType("System.Object"), 5).

		/*Assign the default image for order details */
		defaultImage = new System.Drawing.Bitmap(SEARCH("WinListView/BasicListView/Products16.ICO")).
		ultraListView1:ItemSettings:DefaultImage = defaultImage.
        /* open a query sorted on category */
        queryHandle = itemService:openQuery("eItem","FOR EACH eItem NO-LOCK BY eItem.category1").
        itemBuffer = queryHandle:get-buffer-handle("eItem").
        /*Navigates the query and store the employee records in the ValueList object.*/
        REPEAT:
            IF NOT queryHandle:GET-NEXT(NO-LOCK) THEN LEAVE.
            IF oldCategory NE itemBuffer::category1 THEN
            DO:
                 ASSIGN category    = ultraListView1:Groups:Add(itemBuffer::category1,itemBuffer::category1) .
                        oldCategory = itemBuffer::category1.                                               
            END.

            /*Each field is added in the 'record' System.Array in the same order that the columns
              were defined in the creatListViewColumns method.*/
            record:SetValue(Progress.Util.CastUtil:ToInt32(itemBuffer::Allocated), 0).
            record:SetValue(Progress.Util.CastUtil:ToDecimal(itemBuffer::Price), 1).
            record:SetValue(Progress.Util.CastUtil:ToDecimal(itemBuffer::OnHand), 2).
            record:SetValue(Progress.Util.CastUtil:ToDecimal(itemBuffer::OnOrder), 3).
            record:SetValue(itemBuffer::OnHand > itemBuffer::Allocated , 4).
            currentItem = NEW Infragistics.Win.UltraWinListView.UltraListViewItem(itemBuffer::ItemName, CAST(record, "System.Object[]")).

            /*First adds the item to the UltraListView and then assign the item's category. The item must be
              added to the UltraListView before its category is added.*/
            ultraListView1:Items:Add(currentItem).
            currentItem:GROUP = category.

        END.
        queryHandle:QUERY-CLOSE().
	END METHOD.

	/*------------------------------------------------------------------------------
			Purpose:  																	  
			Notes:  																	  
	------------------------------------------------------------------------------*/
	@VisualDesigner.
	METHOD PRIVATE VOID coView_ValueChanged( INPUT sender AS System.Object, INPUT e AS System.EventArgs ):
        ASSIGN ultraListView1:VIEW = CAST(coView:Value, UltraListViewStyle).
	END METHOD.

	/*------------------------------------------------------------------------------
			Purpose:  																	  
			Notes:  																	  
	------------------------------------------------------------------------------*/
	@VisualDesigner.
	METHOD PRIVATE VOID ultraButton1_Click( INPUT sender AS System.Object, INPUT e AS System.EventArgs ):
            populateListViewWithGroups().
	END METHOD.

	/*------------------------------------------------------------------------------
			Purpose:  																	  
			Notes:  																	  
	------------------------------------------------------------------------------*/
	@VisualDesigner.
	METHOD PRIVATE VOID groupItems_CheckedChanged( INPUT sender AS System.Object, INPUT e AS System.EventArgs ):
	    IF groupItems:CHECKED THEN
            ultraListView1:GroupHeadersVisible = DefaultableBoolean:True.
        ELSE
            ultraListView1:GroupHeadersVisible = DefaultableBoolean:FALSE.
	END METHOD.

END CLASS.