/* 
ABL example derived from Infragistics source, copyright ©1992-2008 Infragistics, 
Inc., Windsor Corporate Park, 50 Millstone Road, Building 200 Suite 150, East 
Windsor, NJ 08520. All rights reserved.
*/
 /*------------------------------------------------------------------------
    File        : frmFixedHeaders
    Purpose     : 
    Syntax      : 
    Description : 
    Author(s)   : hdaniels  - copied/transformed from Infragistic sample
    Created     : Sat May 31 16:18:21 EDT 2008
    Notes       : 
  ----------------------------------------------------------------------*/

USING Progress.Lang.*.
USING Progress.Windows.Form.

 
using WinGrid.SamplesExplorer.*.

CLASS WinGrid.SamplesExplorer.frmFixedHeaders INHERITS Form implements ISamplesBase : 
	
	DEFINE PRIVATE VARIABLE components AS System.ComponentModel.IContainer.

	define public variable Panel1 as System.Windows.Forms.Panel no-undo.
	define private variable groupBoxFixedHeaderBandSettings as System.Windows.Forms.GroupBox no-undo.
	define private variable label2 as System.Windows.Forms.Label no-undo.
	define private variable comboBoxFixedHeaderIndicator as System.Windows.Forms.ComboBox no-undo.
	define private variable ultraColorPicker1 as Infragistics.Win.UltraWinEditors.UltraColorPicker no-undo.
	define private variable label3 as System.Windows.Forms.Label no-undo.
	define private variable buttonFixedHeaderAppearance as System.Windows.Forms.Button no-undo.
	define private variable buttonFixedCellAppearance as System.Windows.Forms.Button no-undo.
	define private variable chkFixedHeaders as System.Windows.Forms.CheckBox no-undo.
	define public variable lblDescription as System.Windows.Forms.Label no-undo.
	define public variable btnTitle as System.Windows.Forms.Button no-undo.
	define private variable groupBoxViewStyle as System.Windows.Forms.GroupBox no-undo.
	define private variable comboBoxViewStyle as System.Windows.Forms.ComboBox no-undo.
	define private variable UltraGrid1 as Infragistics.Win.UltraWinGrid.UltraGrid no-undo.
    
    /* manual defs */
	define private variable fCustOrderDataSource as DataServices.IDataService no-undo.
    define private variable fAppearancePickerForm as AppearancePicker no-undo.
	
	/* Provides a description for the sample. */
	define public property Description as character no-undo 
	get:
	    return lblDescription:Text.
	end.
	
    /* Name of the sample. */
	define public property SampleName as character no-undo
    get:
        return btnTitle:Text. 
    end.
	
    /* Type of sample. */
	define public property SampleType as character no-undo
	get:
	    return "Feature".
	end.     

	/* Preview Picture for Sample */
	define public property PreviewPicture as System.Drawing.Image no-undo
    get:
        return System.Drawing.Image:FromFile(SamplesPath:GraphicsPath + "/Previews/FixedHeaders.png").
    end.  
			
	CONSTRUCTOR PUBLIC frmFixedHeaders (  ):
		
		SUPER().
		session:system-alert-boxes = true.
		InitializeComponent ( ).

	END CONSTRUCTOR.

	DESTRUCTOR PUBLIC frmFixedHeaders ( ):

		IF VALID-OBJECT(components) THEN DO:
			CAST(components, System.IDisposable):Dispose().
		END.

	END DESTRUCTOR.

	
	method private void InitializeComponent ( ):

/* 		NOTE: The following method is generated by the OpenEdge Advanced GUI Visual Designer.

		We strongly suggest that the contents of this method only be modified using the
		Visual Designer to avoid any incompatible modifications.

		Modifying the contents of this method using a code editor will invalidate any support for this file. */

		this-object:Panel1 = new System.Windows.Forms.Panel().
		this-object:groupBoxFixedHeaderBandSettings = new System.Windows.Forms.GroupBox().
		this-object:label2 = new System.Windows.Forms.Label().
		this-object:comboBoxFixedHeaderIndicator = new System.Windows.Forms.ComboBox().
		this-object:ultraColorPicker1 = new Infragistics.Win.UltraWinEditors.UltraColorPicker().
		this-object:label3 = new System.Windows.Forms.Label().
		this-object:buttonFixedHeaderAppearance = new System.Windows.Forms.Button().
		this-object:buttonFixedCellAppearance = new System.Windows.Forms.Button().
		this-object:chkFixedHeaders = new System.Windows.Forms.CheckBox().
		this-object:lblDescription = new System.Windows.Forms.Label().
		this-object:btnTitle = new System.Windows.Forms.Button().
		this-object:groupBoxViewStyle = new System.Windows.Forms.GroupBox().
		this-object:comboBoxViewStyle = new System.Windows.Forms.ComboBox().
		this-object:UltraGrid1 = new Infragistics.Win.UltraWinGrid.UltraGrid().
		this-object:Panel1:SuspendLayout().
		this-object:groupBoxFixedHeaderBandSettings:SuspendLayout().
		cast(this-object:ultraColorPicker1, System.ComponentModel.ISupportInitialize):BeginInit().
		this-object:groupBoxViewStyle:SuspendLayout().
		cast(this-object:UltraGrid1, System.ComponentModel.ISupportInitialize):BeginInit().
		this-object:SuspendLayout().
		/*  */
		/* Panel1 */
		/*  */
		this-object:Panel1:Controls:Add(this-object:groupBoxFixedHeaderBandSettings).
		this-object:Panel1:Controls:Add(this-object:chkFixedHeaders).
		this-object:Panel1:Controls:Add(this-object:lblDescription).
		this-object:Panel1:Controls:Add(this-object:btnTitle).
		this-object:Panel1:Controls:Add(this-object:groupBoxViewStyle).
		this-object:Panel1:Dock = System.Windows.Forms.DockStyle:Top.
		this-object:Panel1:Location = new System.Drawing.Point(0, 0).
		this-object:Panel1:Name = "Panel1".
		this-object:Panel1:Size = new System.Drawing.Size(656, 168).
		this-object:Panel1:TabIndex = 16.
		/*  */
		/* groupBoxFixedHeaderBandSettings */
		/*  */
		this-object:groupBoxFixedHeaderBandSettings:Controls:Add(this-object:label2).
		this-object:groupBoxFixedHeaderBandSettings:Controls:Add(this-object:comboBoxFixedHeaderIndicator).
		this-object:groupBoxFixedHeaderBandSettings:Controls:Add(this-object:ultraColorPicker1).
		this-object:groupBoxFixedHeaderBandSettings:Controls:Add(this-object:label3).
		this-object:groupBoxFixedHeaderBandSettings:Controls:Add(this-object:buttonFixedHeaderAppearance).
		this-object:groupBoxFixedHeaderBandSettings:Controls:Add(this-object:buttonFixedCellAppearance).
		this-object:groupBoxFixedHeaderBandSettings:Enabled = false.
		this-object:groupBoxFixedHeaderBandSettings:Location = new System.Drawing.Point(184, 56).
		this-object:groupBoxFixedHeaderBandSettings:Name = "groupBoxFixedHeaderBandSettings".
		this-object:groupBoxFixedHeaderBandSettings:Size = new System.Drawing.Size(464, 104).
		this-object:groupBoxFixedHeaderBandSettings:TabIndex = 18.
		this-object:groupBoxFixedHeaderBandSettings:TabStop = false.
		this-object:groupBoxFixedHeaderBandSettings:Text = "Fixed Header Settings".
		this-object:groupBoxFixedHeaderBandSettings:UseCompatibleTextRendering = true.
		/*  */
		/* label2 */
		/*  */
		this-object:label2:Location = new System.Drawing.Point(8, 24).
		this-object:label2:Name = "label2".
		this-object:label2:Size = new System.Drawing.Size(144, 17).
		this-object:label2:TabIndex = 24.
		this-object:label2:Text = "Fixed Header Indicator".
		this-object:label2:UseCompatibleTextRendering = true.
		/*  */
		/* comboBoxFixedHeaderIndicator */
		/*  */
		this-object:comboBoxFixedHeaderIndicator:DropDownStyle = System.Windows.Forms.ComboBoxStyle:DropDownList.
		this-object:comboBoxFixedHeaderIndicator:Location = new System.Drawing.Point(152, 24).
		this-object:comboBoxFixedHeaderIndicator:Name = "comboBoxFixedHeaderIndicator".
		this-object:comboBoxFixedHeaderIndicator:Size = new System.Drawing.Size(112, 21).
		this-object:comboBoxFixedHeaderIndicator:TabIndex = 23.
		this-object:comboBoxFixedHeaderIndicator:SelectedIndexChanged:subscribe(this-object:comboBoxFixedHeaderIndicator_SelectedIndexChanged).
		/*  */
		/* ultraColorPicker1 */
		/*  */
		this-object:ultraColorPicker1:Location = new System.Drawing.Point(152, 64).
		this-object:ultraColorPicker1:Name = "ultraColorPicker1".
		this-object:ultraColorPicker1:Size = new System.Drawing.Size(112, 21).
		this-object:ultraColorPicker1:TabIndex = 26.
		this-object:ultraColorPicker1:Text = "Control".
		this-object:ultraColorPicker1:ColorChanged:subscribe(this-object:ultraColorPicker1_ColorChanged).
		/*  */
		/* label3 */
		/*  */
		this-object:label3:Location = new System.Drawing.Point(8, 64).
		this-object:label3:Name = "label3".
		this-object:label3:Size = new System.Drawing.Size(144, 17).
		this-object:label3:TabIndex = 25.
		this-object:label3:Text = "Fixed Cell Separator Color".
		this-object:label3:UseCompatibleTextRendering = true.
		/*  */
		/* buttonFixedHeaderAppearance */
		/*  */
		this-object:buttonFixedHeaderAppearance:Location = new System.Drawing.Point(288, 56).
		this-object:buttonFixedHeaderAppearance:Name = "buttonFixedHeaderAppearance".
		this-object:buttonFixedHeaderAppearance:Size = new System.Drawing.Size(168, 24).
		this-object:buttonFixedHeaderAppearance:TabIndex = 20.
		this-object:buttonFixedHeaderAppearance:Text = "Fixed Header Appearance".
		this-object:buttonFixedHeaderAppearance:UseCompatibleTextRendering = true.
		this-object:buttonFixedHeaderAppearance:Click:subscribe(this-object:buttonFixedHeaderAppearance_Click).
		/*  */
		/* buttonFixedCellAppearance */
		/*  */
		this-object:buttonFixedCellAppearance:Location = new System.Drawing.Point(288, 24).
		this-object:buttonFixedCellAppearance:Name = "buttonFixedCellAppearance".
		this-object:buttonFixedCellAppearance:Size = new System.Drawing.Size(168, 24).
		this-object:buttonFixedCellAppearance:TabIndex = 21.
		this-object:buttonFixedCellAppearance:Text = "Fixed Cell Appearance".
		this-object:buttonFixedCellAppearance:UseCompatibleTextRendering = true.
		this-object:buttonFixedCellAppearance:Click:subscribe(this-object:buttonFixedCellAppearance_Click).
		/*  */
		/* chkFixedHeaders */
		/*  */
		this-object:chkFixedHeaders:Location = new System.Drawing.Point(8, 64).
		this-object:chkFixedHeaders:Name = "chkFixedHeaders".
		this-object:chkFixedHeaders:Size = new System.Drawing.Size(152, 16).
		this-object:chkFixedHeaders:TabIndex = 17.
		this-object:chkFixedHeaders:Text = "Fixed Headers On".
		this-object:chkFixedHeaders:UseCompatibleTextRendering = true.
		this-object:chkFixedHeaders:CheckedChanged:subscribe(this-object:chkFixedHeaders_CheckedChanged).
		/*  */
		/* lblDescription */
		/*  */
		this-object:lblDescription:Dock = System.Windows.Forms.DockStyle:Top.
		this-object:lblDescription:Location = new System.Drawing.Point(0, 32).
		this-object:lblDescription:Name = "lblDescription".
		this-object:lblDescription:Size = new System.Drawing.Size(656, 24).
		this-object:lblDescription:TabIndex = 14.
		this-object:lblDescription:Text = "This sample demonstrates the fixed headers feature. Fixed headers feature allows fixing columns or groups.".
		this-object:lblDescription:UseCompatibleTextRendering = true.
		/*  */
		/* btnTitle */
		/*  */
		this-object:btnTitle:Dock = System.Windows.Forms.DockStyle:Top.
		this-object:btnTitle:FlatStyle = System.Windows.Forms.FlatStyle:Flat.
		this-object:btnTitle:Font = new System.Drawing.Font("Arial", 14.25, System.Drawing.FontStyle:Bold, System.Drawing.GraphicsUnit:Point, Progress.Util.CastUtil:ToByte(0)).
		this-object:btnTitle:Location = new System.Drawing.Point(0, 0).
		this-object:btnTitle:Name = "btnTitle".
		this-object:btnTitle:Size = new System.Drawing.Size(656, 32).
		this-object:btnTitle:TabIndex = 13.
		this-object:btnTitle:Text = "Fixed Headers".
		this-object:btnTitle:TextAlign = System.Drawing.ContentAlignment:MiddleLeft.
		this-object:btnTitle:UseCompatibleTextRendering = true.
		this-object:btnTitle:Click:subscribe(this-object:btnTitle_Click).
		this-object:btnTitle:MouseUp:subscribe(this-object:btnTitle_MouseUp).
		/*  */
		/* groupBoxViewStyle */
		/*  */
		this-object:groupBoxViewStyle:Controls:Add(this-object:comboBoxViewStyle).
		this-object:groupBoxViewStyle:Location = new System.Drawing.Point(8, 88).
		this-object:groupBoxViewStyle:Name = "groupBoxViewStyle".
		this-object:groupBoxViewStyle:Size = new System.Drawing.Size(160, 72).
		this-object:groupBoxViewStyle:TabIndex = 19.
		this-object:groupBoxViewStyle:TabStop = false.
		this-object:groupBoxViewStyle:Text = "View Style".
		this-object:groupBoxViewStyle:UseCompatibleTextRendering = true.
		/*  */
		/* comboBoxViewStyle */
		/*  */
		this-object:comboBoxViewStyle:DropDownStyle = System.Windows.Forms.ComboBoxStyle:DropDownList.
		this-object:comboBoxViewStyle:Location = new System.Drawing.Point(24, 32).
		this-object:comboBoxViewStyle:Name = "comboBoxViewStyle".
		this-object:comboBoxViewStyle:Size = new System.Drawing.Size(120, 21).
		this-object:comboBoxViewStyle:TabIndex = 19.
		this-object:comboBoxViewStyle:SelectedIndexChanged:subscribe(this-object:comboBoxViewStyle_SelectedIndexChanged).
		/*  */
		/* UltraGrid1 */
		/*  */
		this-object:UltraGrid1:Anchor = cast(Progress.Util.EnumHelper:Or(Progress.Util.EnumHelper:Or(Progress.Util.EnumHelper:Or(System.Windows.Forms.AnchorStyles:Top, System.Windows.Forms.AnchorStyles:Bottom), System.Windows.Forms.AnchorStyles:Left), System.Windows.Forms.AnchorStyles:Right), System.Windows.Forms.AnchorStyles).
		this-object:UltraGrid1:Location = new System.Drawing.Point(0, 176).
		this-object:UltraGrid1:Name = "UltraGrid1".
		this-object:UltraGrid1:Size = new System.Drawing.Size(656, 272).
		this-object:UltraGrid1:TabIndex = 17.
		this-object:UltraGrid1:InitializeLayout:subscribe(this-object:UltraGrid1_InitializeLayout).
		/*  */
		/* frmFixedHeaders */
		/*  */
		this-object:ClientSize = new System.Drawing.Size(656, 460).
		this-object:Controls:Add(this-object:UltraGrid1).
		this-object:Controls:Add(this-object:Panel1).
		this-object:Name = "frmFixedHeaders".
		this-object:Text = "FixedHeaders".
		this-object:Load:subscribe(this-object:frmFixedHeaders_Load).
		this-object:FormClosing:subscribe(this-object:frmFixedHeaders_FormClosing).
		this-object:Panel1:ResumeLayout(false).
		this-object:groupBoxFixedHeaderBandSettings:ResumeLayout(false).
		this-object:groupBoxFixedHeaderBandSettings:PerformLayout().
		cast(this-object:ultraColorPicker1, System.ComponentModel.ISupportInitialize):EndInit().
		this-object:groupBoxViewStyle:ResumeLayout(false).
		cast(this-object:UltraGrid1, System.ComponentModel.ISupportInitialize):EndInit().
		this-object:ResumeLayout(false).

	end method.



    method private void FillFixedHeaderIndicatorCombo( ):
		define variable i as integer no-undo.
	    define variable enumValues as System.Array no-undo.
	    define variable type as System.Type no-undo.
        comboBoxFixedHeaderIndicator:Items:Clear( ).
		
		type = Progress.Util.TypeHelper:GetType("Infragistics.Win.UltraWinGrid.FixedHeaderIndicator"). 
        enumValues = System.Enum:GetValues(type).
		do i = 0 to enumValues:Length - 1:                                                                                                       
            comboBoxFixedHeaderIndicator:Items:Add(enumValues:GetValue(i)).	
        end.
		comboBoxFixedHeaderIndicator:SelectedItem = comboBoxFixedHeaderIndicator:Items[0].
	end method.
    
    method private void FillViewStyleCombo( ):
		define variable i as integer no-undo.
	    define variable enumValues as System.Array no-undo.
	    define variable type as System.Type no-undo.
        comboBoxViewStyle:Items:Clear( ).
		
		type = Progress.Util.TypeHelper:GetType("Infragistics.Win.UltraWinGrid.ViewStyleBand"). 
        enumValues = System.Enum:GetValues(type).
		do i = 0 to enumValues:Length - 1:                                                                                                       
            comboBoxViewStyle:Items:Add(enumValues:GetValue(i)).	
        end.
		comboBoxViewStyle:SelectedItem = Infragistics.Win.UltraWinGrid.ViewStyleBand:Vertical.
	end method.
    
    method private void CreateAppearancePicker():
	    if valid-object(fAppearancePickerForm ) then
		do:
		    fAppearancePickerForm:Close().
			fAppearancePickerForm:Dispose().
		end.
		
		fAppearancePickerForm = new AppearancePicker().
	end method.
	
    method protected Progress.Data.BindingSource GetCustOrderSource():   
        if not valid-object(fCustOrderDataSource) then
        do:
            fCustOrderDataSource = new DataServices.CustOrderService().                  
            fCustOrderDataSource:fetchData().                                                 
        end.
        return new Progress.Data.BindingSource(fCustOrderDataSource:DataSetHandle).
    end method. 
	
    /* implements ISamplesBase interface  */
    method public void ShowMe( owner as System.Windows.Forms.Form  ):
/*		InitializeForm().*/
	    if (owner <> ?) then
			this-object:Owner = owner.
		this-object:Show().
	    this-object:CancelButton = btnTitle.
        this-object:CenterToScreen().
		this-object:MaximizeBox = true.
	end method.	

    /* implements ISamplesBase interface  */
    method public void HideMe( ):
        this-object:Hide().   
    end.

	/*------------------------------------------------------------------------------
			Purpose:  																	  
			Notes:  																	  
	------------------------------------------------------------------------------*/
	@VisualDesigner.
	METHOD PRIVATE VOID frmFixedHeaders_Load( INPUT sender AS System.Object, INPUT e AS System.EventArgs ):
		/* Hide the grid caption by setting the Text to empty string. */
	    UltraGrid1:Text = "".

		UltraGrid1:DataSource = GetCustOrderSource().
		RETURN.

	END METHOD.

	/*------------------------------------------------------------------------------
			Purpose:  																	  
			Notes:  																	  
	------------------------------------------------------------------------------*/
	@VisualDesigner.
	METHOD PRIVATE VOID UltraGrid1_InitializeLayout( INPUT sender AS System.Object, INPUT e AS Infragistics.Win.UltraWinGrid.InitializeLayoutEventArgs ):
		/* Set the scroll style to immediate so the rows get scrolled immediately */
		/* when the vertical scrollbar thumb is dragged. */
	 
		e:Layout:ScrollStyle = Infragistics.Win.UltraWinGrid.ScrollStyle:Immediate.

		/* Turn on the fixed headers functionality. */
		 
		e:Layout:UseFixedHeaders = true.

		/* Fix some headers on various bands */
 
		e:Layout:Bands[0]:Columns["Custnum"]:Header:Fixed = true.

		e:Layout:Bands[1]:Columns["Custnum"]:Header:Fixed = true.
		e:Layout:Bands[1]:Columns["OrderNum"]:Header:Fixed = true.

		e:Layout:Bands[2]:Columns["OrderNum"]:Header:Fixed = true.
		e:Layout:Bands[2]:Columns["ItemNum"]:Header:Fixed = true.
		e:Layout:Bands[2]:Columns["LineNum"]:Header:Fixed = true.

		/* Set appearances for fixed headers and cells associated with fixed headers */
		 
		e:Layout:Override:FixedHeaderAppearance:BackColor = System.Drawing.Color:LightYellow.
		e:Layout:Override:FixedHeaderAppearance:ForeColor = System.Drawing.Color:Blue.
		e:Layout:Override:FixedCellAppearance:BackColor = System.Drawing.Color:LightYellow.			
		e:Layout:Override:FixedCellAppearance:ForeColor = System.Drawing.Color:Blue.

		/* Set the color of the line that seperates the fixed cells from the non-fixed cells. */
	 
		e:Layout:Override:FixedCellSeparatorColor = System.Drawing.Color:Blue.

		/* Initialize fields to their proper values. */
		 
		FillViewStyleCombo( ).
 		FillFixedHeaderIndicatorCombo( ). 
		chkFixedHeaders:Checked = e:Layout:UseFixedHeaders.
		ultraColorPicker1:Color = e:Layout:Override:FixedCellSeparatorColor.
        
        /* Expand the first band 0 row to reveal its first band 1 row. */
		if ( ultraGrid1:Rows:Count > 0 ) then
        do: 
            UltraGrid1:Rows[0]:Expanded = true.
            if ultraGrid1:Rows[0]:ChildBands[0]:Rows:Count > 0 then
                UltraGrid1:Rows[0]:ChildBands[0]:Rows[0]:Expanded = true.
        end.
        /*	UltraGrid1:Rows:ExpandAll( true ).*/
		RETURN.

	END METHOD.

	/*------------------------------------------------------------------------------
			Purpose:  																	  
			Notes:  																	  
	------------------------------------------------------------------------------*/
	@VisualDesigner.
	METHOD PRIVATE VOID chkFixedHeaders_CheckedChanged( INPUT sender AS System.Object, INPUT e AS System.EventArgs ):
		/* Turn on the fixed headers functionality */
	    UltraGrid1:DisplayLayout:UseFixedHeaders = chkFixedHeaders:Checked.

		/* Disable fixed header related control if the fixed headers functionality */ 
		/* is turned off. */
	    groupBoxFixedHeaderBandSettings:Enabled = chkFixedHeaders:Checked.
		RETURN.

	END METHOD.

	/*------------------------------------------------------------------------------
			Purpose:  																	  
			Notes:  																	  
	------------------------------------------------------------------------------*/
	@VisualDesigner.
	METHOD PRIVATE VOID ultraColorPicker1_ColorChanged( INPUT sender AS System.Object, INPUT e AS System.EventArgs ):
		UltraGrid1:DisplayLayout:Override:FixedCellSeparatorColor = ultraColorPicker1:Color.
	
		RETURN.

	END METHOD.

	/*------------------------------------------------------------------------------
			Purpose:  																	  
			Notes:  																	  
	------------------------------------------------------------------------------*/
	@VisualDesigner.
	METHOD PRIVATE VOID buttonFixedHeaderAppearance_Click( INPUT sender AS System.Object, INPUT e AS System.EventArgs ):
		CreateAppearancePicker( ).
        fAppearancePickerForm:ShowMe(UltraGrid1:DisplayLayout:Override:FixedHeaderAppearance,"Fixed header appearance").
		RETURN.

	END METHOD.

	/*------------------------------------------------------------------------------
			Purpose:  																	  
			Notes:  																	  
	------------------------------------------------------------------------------*/
	@VisualDesigner.
	METHOD PRIVATE VOID buttonFixedCellAppearance_Click( INPUT sender AS System.Object, INPUT e AS System.EventArgs ):
		CreateAppearancePicker( ).
        fAppearancePickerForm:ShowMe(UltraGrid1:DisplayLayout:Override:FixedCellAppearance,"Fixed cell appearance").
		RETURN.

	END METHOD.

	/*------------------------------------------------------------------------------
			Purpose:  																	  
			Notes:  																	  
	------------------------------------------------------------------------------*/
	@VisualDesigner.
	METHOD PRIVATE VOID comboBoxViewStyle_SelectedIndexChanged( INPUT sender AS System.Object, INPUT e AS System.EventArgs ):
		UltraGrid1:DisplayLayout:ViewStyleBand = cast(comboBoxViewStyle:SelectedItem,Infragistics.Win.UltraWinGrid.ViewStyleBand).
	
		RETURN.

	END METHOD.

	/*------------------------------------------------------------------------------
			Purpose:  																	  
			Notes:  																	  
	------------------------------------------------------------------------------*/
	@VisualDesigner.
	METHOD PRIVATE VOID comboBoxFixedHeaderIndicator_SelectedIndexChanged( INPUT sender AS System.Object, INPUT e AS System.EventArgs ):
		UltraGrid1:DisplayLayout:Override:FixedHeaderIndicator = cast(comboBoxFixedHeaderIndicator:SelectedItem,Infragistics.Win.UltraWinGrid.FixedHeaderIndicator).
        
        /* InSwapDropDown fixed header indicator requires the swapping */
        /* functionality, so turn it on. */
		if Progress.Util.EnumHelper:AreEqual(Infragistics.Win.UltraWinGrid.FixedHeaderIndicator:InSwapDropDown,UltraGrid1:DisplayLayout:Override:FixedHeaderIndicator) then
			UltraGrid1:DisplayLayout:Override:AllowColSwapping = Infragistics.Win.UltraWinGrid.AllowColSwapping:WithinBand.
		else
			UltraGrid1:DisplayLayout:Override:AllowColSwapping = Infragistics.Win.UltraWinGrid.AllowColSwapping:Default.
	
		RETURN.

	END METHOD.
	
	/*------------------------------------------------------------------------------
			Purpose:  																	  
			Notes:  																	  
	------------------------------------------------------------------------------*/
	@VisualDesigner.
	METHOD PRIVATE VOID frmFixedHeaders_FormClosing( INPUT sender AS System.Object, INPUT e AS System.Windows.Forms.FormClosingEventArgs ):
		if valid-object(this-object:Owner) then 
	    do:
	        e:Cancel = true.
		    this-object:Visible = false.
		end.
		RETURN.

	END METHOD.
	
	/*------------------------------------------------------------------------------
			Purpose:  																	  
			Notes:  																	  
	------------------------------------------------------------------------------*/
	@VisualDesigner.
	METHOD PRIVATE VOID btnTitle_Click( INPUT sender AS System.Object, INPUT e AS System.EventArgs ):
		if valid-object(this-object:Owner) then
		    HideMe().   
		RETURN.

	END METHOD.
    
    
	/*------------------------------------------------------------------------------
			Purpose:  																	  
			Notes:  																	  
	------------------------------------------------------------------------------*/
	@VisualDesigner.
	METHOD PRIVATE VOID btnTitle_MouseUp( INPUT sender AS System.Object, INPUT e AS System.Windows.Forms.MouseEventArgs ):
		if valid-object(this-object:Owner) then
		    (cast (this-object:Owner,frmMain)):Navigation(e:Button).
		RETURN.

	END METHOD.
    
 
END CLASS.