 /* 
ABL example derived from Infragistics source, copyright ©1992-2008 Infragistics, 
Inc., Windsor Corporate Park, 50 Millstone Road, Building 200 Suite 150, East 
Windsor, NJ 08520. All rights reserved.
*/
 /*------------------------------------------------------------------------
    File        : frmRowSummaries
    Purpose     : 
    Syntax      : 
    Description : 
    Author(s)   : hdaniels - copied/transformed from Infragistic sample
    Created     : Thu May 22 21:01:58 EDT 2008
    Notes       : 
  ----------------------------------------------------------------------*/

USING Progress.Lang.*.
USING Progress.Windows.Form.

using  WinGrid.SamplesExplorer.*.

CLASS  WinGrid.SamplesExplorer.frmRowSummaries INHERITS Form implements ISamplesBase  : 
	
	DEFINE PRIVATE VARIABLE components AS System.ComponentModel.IContainer.

	define private variable chkExpandAllNodes as System.Windows.Forms.CheckBox no-undo.
	define public variable chkEnableAlphaBlending as System.Windows.Forms.CheckBox no-undo.
	define private variable UltraGrid1 as Infragistics.Win.UltraWinGrid.UltraGrid no-undo.
	define private variable btnRowSummaries as System.Windows.Forms.Button no-undo.
	define public variable lblDescription as System.Windows.Forms.Label no-undo.
	define public variable btnTitle as System.Windows.Forms.Button no-undo.
    
     /* manual defs */
	define private variable fCustOrderDataSource as DataServices.IDataService no-undo.

		/* Provides a description for the sample. */
	define public property Description as character no-undo 
	get:
	    return lblDescription:Text.
	end.
	
    /* Name of the sample. */
	define public property SampleName as character no-undo
    get:
        return btnTitle:Text. 
    end.
	
    /* Type of sample. */
	define public property SampleType as character no-undo
	get:
	    return "Feature".
	end.    
 
	/* Preview Picture for Sample */
	define public property PreviewPicture as System.Drawing.Image no-undo
    get:
        return System.Drawing.Image:FromFile( SamplesPath:GraphicsPath + "/Previews/SummaryRows.png").
    end.   
		
	CONSTRUCTOR PUBLIC frmRowSummaries (  ):
		
		SUPER().
		InitializeComponent ( ).

	END CONSTRUCTOR.

	DESTRUCTOR PUBLIC frmRowSummaries ( ):

		IF VALID-OBJECT(components) THEN DO:
			CAST(components, System.IDisposable):Dispose().
		END.

	END DESTRUCTOR.

	method private void InitializeComponent ( ):

/* 		NOTE: The following method is generated by the OpenEdge Advanced GUI Visual Designer.

		We strongly suggest that the contents of this method only be modified using the
		Visual Designer to avoid any incompatible modifications.

		Modifying the contents of this method using a code editor will invalidate any support for this file. */

		@VisualDesigner.FormMember (NeedsInitialize="true").
		define variable resources as System.Resources.ResXResourceSet no-undo.
		resources = Progress.Util.ResourceHelper:Load("WinGrid/SamplesExplorer/frmRowSummaries.resx", PROPATH).
		this-object:chkExpandAllNodes = new System.Windows.Forms.CheckBox().
		this-object:chkEnableAlphaBlending = new System.Windows.Forms.CheckBox().
		this-object:UltraGrid1 = new Infragistics.Win.UltraWinGrid.UltraGrid().
		this-object:btnRowSummaries = new System.Windows.Forms.Button().
		this-object:lblDescription = new System.Windows.Forms.Label().
		this-object:btnTitle = new System.Windows.Forms.Button().
		cast(this-object:UltraGrid1, System.ComponentModel.ISupportInitialize):BeginInit().
		this-object:SuspendLayout().
		/*  */
		/* chkExpandAllNodes */
		/*  */
		this-object:chkExpandAllNodes:Location = new System.Drawing.Point(120, 93).
		this-object:chkExpandAllNodes:Name = "chkExpandAllNodes".
		this-object:chkExpandAllNodes:Size = new System.Drawing.Size(112, 16).
		this-object:chkExpandAllNodes:TabIndex = 21.
		this-object:chkExpandAllNodes:Text = "Expand All".
		this-object:chkExpandAllNodes:UseCompatibleTextRendering = true.
		this-object:chkExpandAllNodes:CheckedChanged:subscribe(this-object:chkExpandAllNodes_CheckedChanged).
		/*  */
		/* chkEnableAlphaBlending */
		/*  */
		this-object:chkEnableAlphaBlending:Location = new System.Drawing.Point(8, 89).
		this-object:chkEnableAlphaBlending:Name = "chkEnableAlphaBlending".
		this-object:chkEnableAlphaBlending:Size = new System.Drawing.Size(104, 24).
		this-object:chkEnableAlphaBlending:TabIndex = 20.
		this-object:chkEnableAlphaBlending:Text = "AlphaBlending".
		this-object:chkEnableAlphaBlending:UseCompatibleTextRendering = true.
		this-object:chkEnableAlphaBlending:CheckedChanged:subscribe(this-object:chkEnableAlphaBlending_CheckedChanged).
		/*  */
		/* UltraGrid1 */
		/*  */
		this-object:UltraGrid1:Anchor = cast(Progress.Util.EnumHelper:Or(Progress.Util.EnumHelper:Or(Progress.Util.EnumHelper:Or(System.Windows.Forms.AnchorStyles:Top, System.Windows.Forms.AnchorStyles:Bottom), System.Windows.Forms.AnchorStyles:Left), System.Windows.Forms.AnchorStyles:Right), System.Windows.Forms.AnchorStyles).
		this-object:UltraGrid1:Location = new System.Drawing.Point(4, 139).
		this-object:UltraGrid1:Name = "UltraGrid1".
		this-object:UltraGrid1:Size = new System.Drawing.Size(664, 282).
		this-object:UltraGrid1:TabIndex = 22.
		this-object:UltraGrid1:InitializeLayout:subscribe(this-object:UltraGrid1_InitializeLayout).
		/*  */
		/* btnRowSummaries */
		/*  */
		this-object:btnRowSummaries:Location = new System.Drawing.Point(252, 93).
		this-object:btnRowSummaries:Name = "btnRowSummaries".
		this-object:btnRowSummaries:Size = new System.Drawing.Size(132, 24).
		this-object:btnRowSummaries:TabIndex = 23.
		this-object:btnRowSummaries:Text = "Summary Row Settings".
		this-object:btnRowSummaries:UseCompatibleTextRendering = true.
		this-object:btnRowSummaries:Click:subscribe(this-object:btnRowSummaries_Click).
		/*  */
		/* lblDescription */
		/*  */
		this-object:lblDescription:Dock = System.Windows.Forms.DockStyle:Top.
		this-object:lblDescription:Location = new System.Drawing.Point(0, 32).
		this-object:lblDescription:Name = "lblDescription".
		this-object:lblDescription:Size = new System.Drawing.Size(672, 48).
		this-object:lblDescription:TabIndex = 19.
		this-object:lblDescription:Text = resources:GetString("lblDescription.Text").
		this-object:lblDescription:UseCompatibleTextRendering = true.
		/*  */
		/* btnTitle */
		/*  */
		this-object:btnTitle:Dock = System.Windows.Forms.DockStyle:Top.
		this-object:btnTitle:FlatStyle = System.Windows.Forms.FlatStyle:Flat.
		this-object:btnTitle:Font = new System.Drawing.Font("Arial", 14.25, System.Drawing.FontStyle:Bold, System.Drawing.GraphicsUnit:Point, Progress.Util.CastUtil:ToByte(0)).
		this-object:btnTitle:Location = new System.Drawing.Point(0, 0).
		this-object:btnTitle:Name = "btnTitle".
		this-object:btnTitle:Size = new System.Drawing.Size(672, 32).
		this-object:btnTitle:TabIndex = 18.
		this-object:btnTitle:Text = "Summary Rows".
		this-object:btnTitle:TextAlign = System.Drawing.ContentAlignment:MiddleLeft.
		this-object:btnTitle:UseCompatibleTextRendering = true.
		this-object:btnTitle:Click:subscribe(this-object:btnTitle_Click).
		this-object:btnTitle:MouseUp:subscribe(this-object:btnTitle_MouseUp).
		/*  */
		/* frmRowSummaries */
		/*  */
		this-object:ClientSize = new System.Drawing.Size(672, 422).
		this-object:Controls:Add(this-object:chkExpandAllNodes).
		this-object:Controls:Add(this-object:chkEnableAlphaBlending).
		this-object:Controls:Add(this-object:UltraGrid1).
		this-object:Controls:Add(this-object:btnRowSummaries).
		this-object:Controls:Add(this-object:lblDescription).
		this-object:Controls:Add(this-object:btnTitle).
		this-object:Name = "frmRowSummaries".
		this-object:Text = "RowSummaries".
		this-object:Load:subscribe(this-object:frmRowSummaries_Load).
		this-object:FormClosing:subscribe(this-object:frmRowSummaries_FormClosing).
		cast(this-object:UltraGrid1, System.ComponentModel.ISupportInitialize):EndInit().
		this-object:ResumeLayout(false).

	end method.




    method private void FormatCurrencyColumns():
		define variable bands as Infragistics.Win.UltraWinGrid.BandsCollection no-undo.
		define variable bandColumns as Infragistics.Win.UltraWinGrid.ColumnsCollection no-undo.
        define variable oBand as Infragistics.Win.UltraWinGrid.UltraGridBand no-undo.
        define variable oColumn as Infragistics.Win.UltraWinGrid.UltraGridColumn no-undo.
        define variable iBand as integer no-undo.
        define variable iColumn as integer no-undo.
        
		bands = UltraGrid1:DisplayLayout:Bands.
		do iBand = 0 to bands:Count - 1:
		    oBand = cast(bands:getItem(iBand),Infragistics.Win.UltraWinGrid.UltraGridBand).
		    bandColumns = oBand:Columns.
		    do iColumn = 0 to bandColumns:Count - 1:
		        oColumn = cast(bandColumns:getItem(iColumn),Infragistics.Win.UltraWinGrid.UltraGridColumn).
		       
		        if oColumn:DataType:ToString() = "System.Decimal" then
                do:
                    oColumn:CellAppearance:TextHAlign = Infragistics.Win.HAlign:Right.
                    oColumn:Format = "#,###,###.00".
                end.    
		    end.
		end.
	end method.
	
    method protected Progress.Data.BindingSource GetCustOrderSource():   
        if not valid-object(fCustOrderDataSource) then
        do:
            fCustOrderDataSource = new DataServices.CustOrderService().                  
            fCustOrderDataSource:fetchData().                                                 
        end.
        return new Progress.Data.BindingSource(fCustOrderDataSource:DataSetHandle).
    end method. 
    
    /* implements ISamplesBase interface  */
    method public void ShowMe( owner as System.Windows.Forms.Form  ):
/*		InitializeForm().*/
	    if (owner <> ?) then
			this-object:Owner = owner.
		this-object:Show().
	    this-object:CancelButton = btnTitle.
        this-object:CenterToScreen().
		this-object:MaximizeBox = true.
	end.	

    /* implements ISamplesBase interface  */
    method public void HideMe( ):
        this-object:Hide().   
    end.  

	/*------------------------------------------------------------------------------
			Purpose:  																	  
			Notes:  																	  
	------------------------------------------------------------------------------*/
	@VisualDesigner.
	METHOD PRIVATE VOID frmRowSummaries_Load( INPUT sender AS System.Object, INPUT e AS System.EventArgs ):
	    define variable bands as Infragistics.Win.UltraWinGrid.BandsCollection no-undo.
		define variable oBand as Infragistics.Win.UltraWinGrid.UltraGridBand no-undo.
        define variable iBand as integer no-undo.
        
	    /* Hide the grid caption by setting the Text to empty string. */
		UltraGrid1:Text = "".

		UltraGrid1:AlphaBlendMode = Infragistics.Win.AlphaBlendMode:Disabled.
		UltraGrid1:DataSource = GetCustOrderSource().
		
		if UltraGrid1:Rows:Count > 0 then
		    UltraGrid1:Rows[0]:ExpandAll().

		bands = UltraGrid1:DisplayLayout:Bands.
		
		/* Programmatically add summaries for the 'Quantity' and 'Total' column */	
		do iBand = 0 to bands:Count - 1:
		    oBand = cast(bands:getItem(iBand),Infragistics.Win.UltraWinGrid.UltraGridBand).	
		    if oBand:Columns:Exists( "Price" ) then				
		        oBand:Summaries:Add(Infragistics.Win.UltraWinGrid.SummaryType:Average,
		                            oBand:Columns[ "Price" ], 
		                            Infragistics.Win.UltraWinGrid.SummaryPosition:UseSummaryPositionColumn ).

		    if oBand:Columns:Exists( "ExtendedPrice" ) then				
		        oBand:Summaries:Add(Infragistics.Win.UltraWinGrid.SummaryType:Sum,
		                            oBand:Columns[ "ExtendedPrice" ], 
		                            Infragistics.Win.UltraWinGrid.SummaryPosition:UseSummaryPositionColumn ).
		end.
		RETURN.

	END METHOD.

	/*------------------------------------------------------------------------------
			Purpose:  																	  
			Notes:  																	  
	------------------------------------------------------------------------------*/
	@VisualDesigner.
	METHOD PRIVATE VOID UltraGrid1_InitializeLayout( INPUT sender AS System.Object, INPUT e AS Infragistics.Win.UltraWinGrid.InitializeLayoutEventArgs ):
		/*  Hide some key and parent columns */
		e:Layout:Bands[1]:Columns["CustNum"]:Hidden = true.

		e:Layout:Bands[2]:Columns["OrderNum"]:Hidden = true.
		
		e:Layout:Bands[2]:Columns["LineNum"]:Hidden = true.
		e:Layout:Override:RowAppearance:BackColorAlpha = Infragistics.Win.Alpha:Transparent.

		/*	use the same appearance for alternate rows */
		e:Layout:Override:RowAlternateAppearance = e:Layout:Override:RowAlternateAppearance.
		e:Layout:Override:CellAppearance:BackColorAlpha = Infragistics.Win.Alpha:UseAlphaLevel.
		e:Layout:Override:CellAppearance:AlphaLevel = 192.

		e:Layout:Override:HeaderAppearance:AlphaLevel = 192.
		e:Layout:Override:HeaderAppearance:BackColorAlpha = Infragistics.Win.Alpha:UseAlphaLevel.

		FormatCurrencyColumns().

		/*   Set up the grid to display row summaries for all columns */
		e:Layout:Override:AllowRowSummaries = Infragistics.Win.UltraWinGrid.AllowRowSummaries:True.
	
		RETURN.

	END METHOD.

	/*------------------------------------------------------------------------------
			Purpose:  																	  
			Notes:  																	  
	------------------------------------------------------------------------------*/
	@VisualDesigner.
	METHOD PRIVATE VOID chkEnableAlphaBlending_CheckedChanged( INPUT sender AS System.Object, INPUT e AS System.EventArgs ):
		/*	enable/disable alphablending */
		if chkEnableAlphaBlending:Checked then
        do: 
			UltraGrid1:DisplayLayout:Appearance:ImageBackground = System.Drawing.Image:FromFile(SamplesPath:GraphicsPath + "/Backgrounds/nasa_saturn_montage.jpg").
			UltraGrid1:DisplayLayout:Appearance:ImageBackgroundOrigin = Infragistics.Win.ImageBackgroundOrigin:Form.
			UltraGrid1:DisplayLayout:Appearance:ImageBackgroundStyle = Infragistics.Win.ImageBackgroundStyle:Tiled.
            UltraGrid1:AlphaBlendMode = Infragistics.Win.AlphaBlendMode:Optimized.
		end.
		else 
		do:
			UltraGrid1:AlphaBlendMode = Infragistics.Win.AlphaBlendMode:Disabled.
			UltraGrid1:DisplayLayout:Appearance:ImageBackground = ?.
		end.	
		RETURN.

	END METHOD.

	/*------------------------------------------------------------------------------
			Purpose:  																	  
			Notes:  																	  
	------------------------------------------------------------------------------*/
	@VisualDesigner.
	METHOD PRIVATE VOID chkExpandAllNodes_CheckedChanged( INPUT sender AS System.Object, INPUT e AS System.EventArgs ):
		if chkExpandAllNodes:Checked then
			UltraGrid1:Rows:ExpandAll(true).
		else
		    UltraGrid1:Rows:CollapseAll(true).
		RETURN.
	END METHOD.

	/*------------------------------------------------------------------------------
			Purpose:  																	  
			Notes:  																	  
	------------------------------------------------------------------------------*/
	@VisualDesigner.
	METHOD PRIVATE VOID btnRowSummaries_Click( INPUT sender AS System.Object, INPUT e AS System.EventArgs ):
		define variable frmSettings as frmRowSummarySettings no-undo.
		frmSettings = new frmRowSummarySettings().
	    frmSettings:ShowMe( UltraGrid1 ).
		RETURN.
	END METHOD.
	
	/*------------------------------------------------------------------------------
			Purpose:  																	  
			Notes:  	  																  
	------------------------------------------------------------------------------*/
	@VisualDesigner.
	METHOD PRIVATE VOID frmRowSummaries_FormClosing( INPUT sender AS System.Object, INPUT e AS System.Windows.Forms.FormClosingEventArgs ):
		if valid-object(this-object:Owner) then
        do:
            e:Cancel = true.
    		this-object:Visible = false.
		end.
		RETURN.
	END METHOD.
	 
	/*------------------------------------------------------------------------------
			Purpose:  																	  
			Notes:  																	  
	------------------------------------------------------------------------------*/
	@VisualDesigner.
	METHOD PRIVATE VOID btnTitle_Click( INPUT sender AS System.Object, INPUT e AS System.EventArgs ):
		if valid-object(this-object:Owner) then
            HideMe().
		RETURN.
	END METHOD.
	
	/*------------------------------------------------------------------------------
			Purpose:  																	  
			Notes:  																	  
	------------------------------------------------------------------------------*/
	@VisualDesigner.
	METHOD PRIVATE VOID btnTitle_MouseUp( INPUT sender AS System.Object, INPUT e AS System.Windows.Forms.MouseEventArgs ):
		if valid-object(this-object:Owner) then
		    (cast (this-object:Owner,frmMain)):Navigation(e:Button).
		RETURN.
	END METHOD. 
END CLASS.