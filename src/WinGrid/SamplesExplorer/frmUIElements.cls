 /* 
ABL example derived from Infragistics source, copyright ©1992-2008 Infragistics, 
Inc., Windsor Corporate Park, 50 Millstone Road, Building 200 Suite 150, East 
Windsor, NJ 08520. All rights reserved.
*/
 /*------------------------------------------------------------------------
    File        : frmUIElements
    Purpose     : 
    Syntax      : 
    Description :  
    Author(s)   : hdaniels - copied/transformed from Infragistic sample
    Created     : Mon May 19 18:58:20 EDT 2008
    Notes       : 
  ----------------------------------------------------------------------*/

USING Progress.Lang.*.
USING Progress.Windows.Form.

using WinGrid.SamplesExplorer.*.

CLASS WinGrid.SamplesExplorer.frmUIElements INHERITS Form implements ISamplesBase  : 
	
	DEFINE PRIVATE VARIABLE components AS System.ComponentModel.IContainer.


	define public variable Panel1 as System.Windows.Forms.Panel no-undo.
	define public variable chkEnableAlphaBlending as System.Windows.Forms.CheckBox no-undo.
	define public variable lblDescription as System.Windows.Forms.Label no-undo.
	define public variable btnTitle as System.Windows.Forms.Button no-undo.

	define public variable gridUIElements as Infragistics.Win.UltraWinGrid.UltraGrid no-undo.


	define public variable Panel2 as System.Windows.Forms.Panel no-undo.
	define public variable txtAncestors as System.Windows.Forms.TextBox no-undo.
	define public variable Splitter1 as System.Windows.Forms.Splitter no-undo.
	define public variable lvwProperties as System.Windows.Forms.ListView no-undo.

	define private variable splitter2 as System.Windows.Forms.Splitter no-undo.
	CONSTRUCTOR PUBLIC frmUIElements (  ):
		
		SUPER().
		InitializeComponent ().
        InitializeDataSource().
	END CONSTRUCTOR.

	DESTRUCTOR PUBLIC frmUIElements ( ):

		IF VALID-OBJECT(components) THEN DO:
			CAST(components, System.IDisposable):Dispose().
		END.

	END DESTRUCTOR.

     /** manual code */
	define private static variable ALPHA_LEVEL_ROW as int init 192.
	define private static variable ALPHA_LEVEL_CELL as int init 192.
	define private static variable ALPHA_LEVEL_HEADER as int init 192.
	define private static variable ALPHA_LEVEL_ADDNEW as int init 192.
    
    define private variable fCustOrderBindingSource as Progress.Data.BindingSource no-undo.    
    define private variable fCustOrderDataSource as DataServices.IDataService no-undo.

		/* Provides a description for the sample. */
	define public property Description as character no-undo 
	get:
	    return lblDescription:Text.
	end.
	
    /* Name of the sample. */
	define public property SampleName as character no-undo
    get:
        return btnTitle:Text. 
    end.
	
    /* Type of sample. */
	define public property SampleType as character no-undo
	get:
	    return "Feature".
	end.    

	/* Preview Picture for Sample */
	define public property PreviewPicture as System.Drawing.Image no-undo
    get:
        /* TODO: store path in static */
        return System.Drawing.Image:FromFile( SamplesPath:GraphicsPath + "/Previews/UIElements.png").
    end.   


	method private void InitializeComponent ( ):

/* 		NOTE: The following method is generated by the OpenEdge Advanced GUI Visual Designer.

		We strongly suggest that the contents of this method only be modified using the
		Visual Designer to avoid any incompatible modifications.

		Modifying the contents of this method using a code editor will invalidate any support for this file. */

		this-object:Panel1 = new System.Windows.Forms.Panel().
		this-object:chkEnableAlphaBlending = new System.Windows.Forms.CheckBox().
		this-object:lblDescription = new System.Windows.Forms.Label().
		this-object:btnTitle = new System.Windows.Forms.Button().
		this-object:gridUIElements = new Infragistics.Win.UltraWinGrid.UltraGrid().
		this-object:Panel2 = new System.Windows.Forms.Panel().
		this-object:txtAncestors = new System.Windows.Forms.TextBox().
		this-object:Splitter1 = new System.Windows.Forms.Splitter().
		this-object:lvwProperties = new System.Windows.Forms.ListView().
		this-object:splitter2 = new System.Windows.Forms.Splitter().
		this-object:Panel1:SuspendLayout().
		cast(this-object:gridUIElements, System.ComponentModel.ISupportInitialize):BeginInit().
		this-object:Panel2:SuspendLayout().
		this-object:SuspendLayout().
		/*  */
		/* Panel1 */
		/*  */
		this-object:Panel1:Controls:Add(this-object:chkEnableAlphaBlending).
		this-object:Panel1:Controls:Add(this-object:lblDescription).
		this-object:Panel1:Controls:Add(this-object:btnTitle).
		this-object:Panel1:Dock = System.Windows.Forms.DockStyle:Top.
		this-object:Panel1:Location = new System.Drawing.Point(0, 0).
		this-object:Panel1:Name = "Panel1".
		this-object:Panel1:Size = new System.Drawing.Size(688, 96).
		this-object:Panel1:TabIndex = 21.
		/*  */
		/* chkEnableAlphaBlending */
		/*  */
		this-object:chkEnableAlphaBlending:Location = new System.Drawing.Point(8, 64).
		this-object:chkEnableAlphaBlending:Name = "chkEnableAlphaBlending".
		this-object:chkEnableAlphaBlending:Size = new System.Drawing.Size(104, 24).
		this-object:chkEnableAlphaBlending:TabIndex = 15.
		this-object:chkEnableAlphaBlending:Text = "AlphaBlending".
		this-object:chkEnableAlphaBlending:UseCompatibleTextRendering = true.
		this-object:chkEnableAlphaBlending:CheckedChanged:subscribe(this-object:chkEnableAlphaBlending_CheckedChanged).
		/*  */
		/* lblDescription */
		/*  */
		this-object:lblDescription:Dock = System.Windows.Forms.DockStyle:Top.
		this-object:lblDescription:Location = new System.Drawing.Point(0, 32).
		this-object:lblDescription:Name = "lblDescription".
		this-object:lblDescription:Size = new System.Drawing.Size(688, 24).
		this-object:lblDescription:TabIndex = 14.
		this-object:lblDescription:Text = "This sample demonstrates UIElements.".
		this-object:lblDescription:UseCompatibleTextRendering = true.
		/*  */
		/* btnTitle */
		/*  */
		this-object:btnTitle:Dock = System.Windows.Forms.DockStyle:Top.
		this-object:btnTitle:FlatStyle = System.Windows.Forms.FlatStyle:Flat.
		this-object:btnTitle:Font = new System.Drawing.Font("Arial", 14.25, System.Drawing.FontStyle:Bold, System.Drawing.GraphicsUnit:Point, Progress.Util.CastUtil:ToByte(0)).
		this-object:btnTitle:Location = new System.Drawing.Point(0, 0).
		this-object:btnTitle:Name = "btnTitle".
		this-object:btnTitle:Size = new System.Drawing.Size(688, 32).
		this-object:btnTitle:TabIndex = 13.
		this-object:btnTitle:Text = "UIElements".
		this-object:btnTitle:TextAlign = System.Drawing.ContentAlignment:MiddleLeft.
		this-object:btnTitle:UseCompatibleTextRendering = true.
		this-object:btnTitle:MouseUp:subscribe(this-object:btnTitle_MouseUp).
		/*  */
		/* gridUIElements */
		/*  */
		this-object:gridUIElements:Location = new System.Drawing.Point(0, 102).
		this-object:gridUIElements:Name = "gridUIElements".
		this-object:gridUIElements:Size = new System.Drawing.Size(688, 168).
		this-object:gridUIElements:TabIndex = 24.
		this-object:gridUIElements:InitializeLayout:subscribe(this-object:gridUIElements_InitializeLayout).
		this-object:gridUIElements:MouseLeaveElement:subscribe(this-object:gridUIElements_MouseLeaveElement).
		this-object:gridUIElements:MouseEnterElement:subscribe(this-object:gridUIElements_MouseEnterElement).
		/*  */
		/* Panel2 */
		/*  */
		this-object:Panel2:Controls:Add(this-object:txtAncestors).
		this-object:Panel2:Controls:Add(this-object:Splitter1).
		this-object:Panel2:Controls:Add(this-object:lvwProperties).
		this-object:Panel2:Dock = System.Windows.Forms.DockStyle:Bottom.
		this-object:Panel2:Location = new System.Drawing.Point(0, 285).
		this-object:Panel2:Name = "Panel2".
		this-object:Panel2:Size = new System.Drawing.Size(688, 176).
		this-object:Panel2:TabIndex = 25.
		/*  */
		/* txtAncestors */
		/*  */
		this-object:txtAncestors:Dock = System.Windows.Forms.DockStyle:Fill.
		this-object:txtAncestors:Location = new System.Drawing.Point(472, 0).
		this-object:txtAncestors:Multiline = true.
		this-object:txtAncestors:Name = "txtAncestors".
		this-object:txtAncestors:Size = new System.Drawing.Size(216, 176).
		this-object:txtAncestors:TabIndex = 3.
		this-object:txtAncestors:WordWrap = false.
		/*  */
		/* Splitter1 */
		/*  */
		this-object:Splitter1:Location = new System.Drawing.Point(464, 0).
		this-object:Splitter1:Name = "Splitter1".
		this-object:Splitter1:Size = new System.Drawing.Size(8, 176).
		this-object:Splitter1:TabIndex = 2.
		this-object:Splitter1:TabStop = false.
		/*  */
		/* lvwProperties */
		/*  */
		this-object:lvwProperties:Dock = System.Windows.Forms.DockStyle:Left.
		this-object:lvwProperties:Location = new System.Drawing.Point(0, 0).
		this-object:lvwProperties:Name = "lvwProperties".
		this-object:lvwProperties:Size = new System.Drawing.Size(464, 176).
		this-object:lvwProperties:TabIndex = 0.
		this-object:lvwProperties:UseCompatibleStateImageBehavior = false.
		/*  */
		/* splitter2 */
		/*  */
		this-object:splitter2:Dock = System.Windows.Forms.DockStyle:Bottom.
		this-object:splitter2:Location = new System.Drawing.Point(0, 277).
		this-object:splitter2:Name = "splitter2".
		this-object:splitter2:Size = new System.Drawing.Size(688, 8).
		this-object:splitter2:TabIndex = 26.
		this-object:splitter2:TabStop = false.
		this-object:splitter2:Move:subscribe(this-object:splitter2_Move).
		/*  */
		/* frmUIElements */
		/*  */
		this-object:ClientSize = new System.Drawing.Size(688, 461).
		this-object:Controls:Add(this-object:splitter2).
		this-object:Controls:Add(this-object:Panel2).
		this-object:Controls:Add(this-object:gridUIElements).
		this-object:Controls:Add(this-object:Panel1).
		this-object:Name = "frmUIElements".
		this-object:Text = "UIElements".
		this-object:Load:subscribe(this-object:frmUIElements_Load).
		this-object:FormClosing:subscribe(this-object:frmUIElements_FormClosing).
		this-object:Resize:subscribe(this-object:frmUIElements_Resize).
		this-object:Panel1:ResumeLayout(false).
		cast(this-object:gridUIElements, System.ComponentModel.ISupportInitialize):EndInit().
		this-object:Panel2:ResumeLayout(false).
		this-object:Panel2:PerformLayout().
		this-object:ResumeLayout(false).

	end method.


    method private void InitializeDataSource():   
        fCustOrderDataSource = new DataServices.CustOrderService().                  
        fCustOrderDataSource:fetchData().                                                 
       
        fCustOrderBindingSource = new Progress.Data.BindingSource().
        fCustOrderBindingSource:Handle = fCustOrderDataSource:DataSetHandle. 
        
    end method.   
    
    method private void PositionGrid():
		def var topPos as int no-undo.
		def var pnlHeight as int no-undo.
		topPos = Panel1:Height + 1.				
		pnlHeight = this-object:Height - Panel1:Height - Panel2:Height - splitter2:Height.
		gridUIElements:SetBounds(0, topPos, this-object:Width - 12, pnlHeight - 30).
    end method.
    
    method private void HighlightUIElement( element as Infragistics.Win.UIElement ):
        define variable oContext as System.Object no-undo. 
	    define variable oHeader  as Infragistics.Win.UltraWinGrid.ColumnHeader no-undo.
	    define variable oCell    as Infragistics.Win.UltraWinGrid.UltraGridCell no-undo.
	    define variable oRow     as Infragistics.Win.UltraWinGrid.UltraGridRow no-undo.
	    
        if not valid-object(element:Parent) then
			return.
         
        /* if the element is a DependentTextUIElement, or a CellUIElement */
		if element:GetType() = Progress.Util.TypeHelper:GetType("Infragistics.Win.DependentTextUIElement")
		or element:GetType() = Progress.Util.TypeHelper:GetType("Infragistics.Win.UltraWinGrid.CellUIElement") then
		do:
            oContext = element:GetContext(Progress.Util.TypeHelper:GetType("Infragistics.Win.UltraWinGrid.UltraGridCell")).
            if valid-object(oContext) then
			do:
                oCell = cast(oContext,Infragistics.Win.UltraWinGrid.UltraGridCell).
                oCell:Appearance:BackColor = System.Drawing.Color:Red.
                oCell:Appearance:ForeColor = System.Drawing.Color:White.
            end.
        end.      
        
        /*  if the element is a TextUIElement, or a HeaderUIElement */
        if element:GetType() = Progress.Util.TypeHelper:GetType("Infragistics.Win.TextUIElement")
		or element:GetType() = Progress.Util.TypeHelper:GetType("Infragistics.Win.UltraWinGrid.HeaderUIElement") then
		do:
            oContext = element:GetContext(Progress.Util.TypeHelper:GetType("Infragistics.Win.UltraWinGrid.ColumnHeader")).
            if valid-object(oContext) then
			do:
                oHeader = cast(oContext,Infragistics.Win.UltraWinGrid.ColumnHeader).
                oHeader:Appearance:BackColor = System.Drawing.Color:Red.
                oHeader:Appearance:ForeColor = System.Drawing.Color:White.
                return.
			end.
		end.
        
        /*   if the element is a RowAutoPreviewUIElement */
        if  element:GetType() = Progress.Util.TypeHelper:GetType("Infragistics.Win.TextUIElement")
		and element:Parent:GetType() = Progress.Util.TypeHelper:GetType("Infragistics.Win.UltraWinGrid.RowAutoPreviewUIElement") then
		do:
            oContext = element:GetContext(Progress.Util.TypeHelper:GetType("Infragistics.Win.UltraWinGrid.UltraGridRow")).
            if valid-object(oContext) then
			do:
                oRow  = cast(oContext,Infragistics.Win.UltraWinGrid.UltraGridRow).
                oRow:PreviewAppearance:BackColor = System.Drawing.Color:Red.
                oRow:PreviewAppearance:ForeColor = System.Drawing.Color:White.
                return.
			end.
		end.
       
        /*   if the element is a RowSelectorUIElement */
        if  element:GetType() = Progress.Util.TypeHelper:GetType("Infragistics.Win.UltraWinGrid.RowSelectorUIElement") then
        do:	      
            oContext = element:GetContext(Progress.Util.TypeHelper:GetType("Infragistics.Win.UltraWinGrid.UltraGridRow")).
            if valid-object(oContext) then
			do:
                oRow  = cast(oContext,Infragistics.Win.UltraWinGrid.UltraGridRow).
                oRow:RowSelectorAppearance:BackColor = System.Drawing.Color:Red.
                oRow:RowSelectorAppearance:ForeColor = System.Drawing.Color:White.
                return.
			end.
		end.
        
        /*   if the element has a row as an ancestor */
        oContext = element:GetContext(Progress.Util.TypeHelper:GetType("Infragistics.Win.UltraWinGrid.UltraGridRow")).
	    if valid-object(oContext) then
		do:
            oRow  = cast(oContext,Infragistics.Win.UltraWinGrid.UltraGridRow).
            oRow:Appearance:BackColor = System.Drawing.SystemColors:Highlight.
            oRow:Appearance:ForeColor = System.Drawing.SystemColors:HighlightText.
            /* oRow.GetUIElement().Invalidate() */
            return.
		end.
  	end method.
    
    method private void UnHighlightUIElement(element as Infragistics.Win.UIElement):
	    define variable oContext as System.Object no-undo. 
	    define variable oHeader  as Infragistics.Win.UltraWinGrid.ColumnHeader no-undo.
	    define variable oCell    as Infragistics.Win.UltraWinGrid.UltraGridCell no-undo.
	    define variable oRow     as Infragistics.Win.UltraWinGrid.UltraGridRow no-undo.
	     
	    if not valid-object(element:Parent) then
			return.

        /*   if the element is a DependentTextUIElement, or a CellUIElement */
		if element:GetType() = Progress.Util.TypeHelper:GetType("Infragistics.Win.DependentTextUIElement")
		or element:GetType() = Progress.Util.TypeHelper:GetType("Infragistics.Win.UltraWinGrid.CellUIElement") then
		do:
            oContext = element:GetContext(Progress.Util.TypeHelper:GetType("Infragistics.Win.UltraWinGrid.UltraGridCell")).
            if valid-object(oContext) then
			do:
                oCell = cast(oContext,Infragistics.Win.UltraWinGrid.UltraGridCell).
                oCell:Appearance:BackColor = gridUIElements:DisplayLayout:Override:CellAppearance:BackColor.
                oCell:Appearance:ForeColor = gridUIElements:DisplayLayout:Override:CellAppearance:ForeColor.
                /*oCell.GetUIElement().Invalidate(); */
                return.
			end.
		end.
        
        /*  if the element is a TextUIElement, or a HeaderUIElement */
        if element:GetType() = Progress.Util.TypeHelper:GetType("Infragistics.Win.TextUIElement")
		or element:GetType() = Progress.Util.TypeHelper:GetType("Infragistics.Win.UltraWinGrid.HeaderUIElement") then
		do:
            oContext = element:GetContext(Progress.Util.TypeHelper:GetType("Infragistics.Win.UltraWinGrid.ColumnHeader")).
            if valid-object(oContext) then
			do:
                oHeader = cast(oContext,Infragistics.Win.UltraWinGrid.ColumnHeader).
                oHeader:Appearance:BackColor = gridUIElements:DisplayLayout:Override:HeaderAppearance:BackColor.
                oHeader:Appearance:ForeColor = gridUIElements:DisplayLayout:Override:HeaderAppearance:ForeColor.
                /* oHeader.GetUIElement().Invalidate(); */
                return.
			end.
		end.

        /*   if the element is a RowAutoPreviewUIElement */
        if  element:GetType() = Progress.Util.TypeHelper:GetType("Infragistics.Win.TextUIElement")
		and element:Parent:GetType() = Progress.Util.TypeHelper:GetType("Infragistics.Win.UltraWinGrid.RowAutoPreviewUIElement") then
		do:
            oContext = element:GetContext(Progress.Util.TypeHelper:GetType("Infragistics.Win.UltraWinGrid.UltraGridRow")).
            if valid-object(oContext) then
			do:
                oRow  = cast(oContext,Infragistics.Win.UltraWinGrid.UltraGridRow).
                oRow:PreviewAppearance:BackColor = gridUIElements:DisplayLayout:Override:RowPreviewAppearance:BackColor.
                oRow:PreviewAppearance:ForeColor = gridUIElements:DisplayLayout:Override:RowPreviewAppearance:ForeColor.
                /* oRow.GetUIElement().Invalidate() */
                return.
			end.
		end.
        
        /*   if the element is a RowSelectorUIElement */
        if  element:GetType() = Progress.Util.TypeHelper:GetType("Infragistics.Win.UltraWinGrid.RowSelectorUIElement") then
        do:	      
            oContext = element:GetContext(Progress.Util.TypeHelper:GetType("Infragistics.Win.UltraWinGrid.UltraGridRow")).
            if valid-object(oContext) then
			do:
                oRow  = cast(oContext,Infragistics.Win.UltraWinGrid.UltraGridRow).
                oRow:RowSelectorAppearance:BackColor = gridUIElements:DisplayLayout:Override:RowSelectorAppearance:BackColor.
                oRow:RowSelectorAppearance:ForeColor = gridUIElements:DisplayLayout:Override:RowSelectorAppearance:ForeColor.
                /* oRow.GetUIElement().Invalidate() */
                return.
			end.
		end.
   
        /*   if the element has a row as an ancestor */
        oContext = element:GetContext(Progress.Util.TypeHelper:GetType("Infragistics.Win.UltraWinGrid.UltraGridRow")).
	    if valid-object(oContext) then
		do:
            oRow  = cast(oContext,Infragistics.Win.UltraWinGrid.UltraGridRow).
            oRow:Appearance:BackColor = gridUIElements:DisplayLayout:Override:RowAppearance:BackColor.
            oRow:Appearance:ForeColor = gridUIElements:DisplayLayout:Override:RowAppearance:ForeColor.
            /* oRow.GetUIElement().Invalidate() */
            return.
		end.
	end method.
    
    method private void DisplayUIElementProperties(element as Infragistics.Win.UIElement ):
		lvwProperties:Items[0]:SubItems[1]:Text = if element:Adjustable then "True" else "False".
		lvwProperties:Items[1]:SubItems[1]:Text = element:BorderSides:ToString().
		lvwProperties:Items[2]:SubItems[1]:Text = element:BorderStyle:ToString().
		lvwProperties:Items[3]:SubItems[1]:Text = element:ClipRect:ToString().		
		
		/*
		try
		 */
			lvwProperties:Items[4]:SubItems[1]:Text = element:Cursor:ToString().
		 /*
		catch ( Exception )
		 
			lvwProperties:Items[4]:SubItems[1]:Text = "Custom".
		 */
		lvwProperties:Items[5]:SubItems[1]:Text = if element:Enabled then "True" else "False".
		lvwProperties:Items[6]:SubItems[1]:Text = if element:HasChildElements then "True" else "False".
		lvwProperties:Items[7]:SubItems[1]:Text = if element:IsDrawing then "True" else "False".
		lvwProperties:Items[8]:SubItems[1]:Text = if element:IsElementDrawn then "True" else "False".
		lvwProperties:Items[9]:SubItems[1]:Text = if element:IsFullyVisible then "True" else "False".
		lvwProperties:Items[10]:SubItems[1]:Text = string(element:NestDepth).

		if not valid-object(element:Parent) then
			lvwProperties:Items[11]:SubItems[1]:Text = ("Nothing").
		else
			lvwProperties:Items[11]:SubItems[1]:Text = element:Parent:ToString().

		lvwProperties:Items[12]:SubItems[1]:Text = element:Rect:ToString().
		lvwProperties:Items[13]:SubItems[1]:Text = string(element:RectInsideBorders).
		lvwProperties:Items[14]:SubItems[1]:Text = element:Region:ToString().
               
    end method.
    
    method private void DisplayUIElementAncestors( element as Infragistics.Win.UIElement):
	    define variable strTab as character no-undo.
	    define variable shortName as character no-undo.
	    define variable removeString as character no-undo.
	    
	    assign
	        txtAncestors:Text = ""
            removeString = "Infragistics.Win.".
            
        do while valid-object(element):
		    shortName = element:GetType():ToString().
            if shortName begins removeString then
                shortName = substring(shortName,length(removeString) + 1).

			if txtAncestors:Text = "" then
               txtAncestors:Text = shortName + "~r~n".
            else 
            do:
                assign
				    strTab = strTab + " "
                    txtAncestors:Text = txtAncestors:Text + strTab + shortName + "~r~n".
			end.
			element = element:Parent.
		end.
	end method.
    
    /* implements ISamplesBase interface  */
    method public void ShowMe( owner as System.Windows.Forms.Form  ):
/*		InitializeForm().*/
	    if (owner <> ?) then
			this-object:Owner = owner.
		this-object:Show().
	    this-object:CancelButton = btnTitle.
        this-object:CenterToScreen().
		this-object:MaximizeBox = true.
	end.	

    /* implements ISamplesBase interface  */
    method public void HideMe( ):
        this-object:Hide().   
    end.
     

	/*------------------------------------------------------------------------------
			Purpose:  																	  
			Notes:  																	  
	------------------------------------------------------------------------------*/
	@VisualDesigner.
	METHOD PRIVATE VOID splitter2_Move( INPUT sender AS System.Object, INPUT e AS System.EventArgs ):
		PositionGrid().
		RETURN.

	END METHOD.

	/*------------------------------------------------------------------------------
			Purpose:  																	  
			Notes:  																	  
	------------------------------------------------------------------------------*/
	@VisualDesigner.
	METHOD PRIVATE VOID chkEnableAlphaBlending_CheckedChanged( INPUT sender AS System.Object, INPUT e AS System.EventArgs ):
		/*	enable/disable alphablending */
		if chkEnableAlphaBlending:Checked then
        do: 
		    /*string strGraphicsPath = clsSamplesPath.GraphicsPath; */
			gridUIElements:DisplayLayout:Appearance:ImageBackground = System.Drawing.Image:FromFile(SamplesPath:GraphicsPath + "/Backgrounds/globe.jpg").
			gridUIElements:DisplayLayout:Appearance:ImageBackgroundOrigin = Infragistics.Win.ImageBackgroundOrigin:Form.
			gridUIElements:DisplayLayout:Appearance:ImageBackgroundStyle = Infragistics.Win.ImageBackgroundStyle:Tiled.
            gridUIElements:AlphaBlendMode = Infragistics.Win.AlphaBlendMode:Optimized.
		end.
		else 
		do:
			gridUIElements:AlphaBlendMode = Infragistics.Win.AlphaBlendMode:Disabled.
			gridUIElements:DisplayLayout:Appearance:ImageBackground = ?.
		end.	
		RETURN.

	END METHOD.

	/*------------------------------------------------------------------------------
			Purpose:  																	  
			Notes:  																	  
	------------------------------------------------------------------------------*/
	@VisualDesigner.
	METHOD PRIVATE VOID frmUIElements_Load( INPUT sender AS System.Object, INPUT e AS System.EventArgs ):
		define variable oRow as Infragistics.Win.UltraWinGrid.UltraGridRow no-undo.
		
		/* Hide the grid caption by setting the Text to empty string.*/
		gridUIElements:Text = "".

		gridUIElements:DataSource = fCustOrderBindingSource.
		gridUIElements:AlphaBlendMode = Infragistics.Win.AlphaBlendMode:Disabled.

		/*   Expand the first row in the grid */
		oRow = gridUIElements:GetRow(Infragistics.Win.UltraWinGrid.ChildRow:First).
		oRow:ExpandAll().
		
		/*   Configure the listview */
		lvwProperties:LabelEdit = false.
		lvwProperties:View = System.Windows.Forms.View:Details.

		lvwProperties:Columns:Add("Name", 100, System.Windows.Forms.HorizontalAlignment:Left).
		lvwProperties:Columns:Add("Value", 400, System.Windows.Forms.HorizontalAlignment:Left).

		/*   Add an item for each property that we are going to display */
		lvwProperties:Items:Clear().

		lvwProperties:Items:Add("Adjustable").
		lvwProperties:Items:Add("BorderSides").
		lvwProperties:Items:Add("BorderStyle").
		lvwProperties:Items:Add("ClipRect").
		lvwProperties:Items:Add("Cursor").
		lvwProperties:Items:Add("Enabled").
		lvwProperties:Items:Add("HasChildElements").
		lvwProperties:Items:Add("IsDrawing").
		lvwProperties:Items:Add("IsElementDrawn").
		lvwProperties:Items:Add("IsFullyVisible").
		lvwProperties:Items:Add("NestDepth").
		lvwProperties:Items:Add("Parent").
		lvwProperties:Items:Add("Rect").
		lvwProperties:Items:Add("RectInsideBorders").
		lvwProperties:Items:Add("Region").

		lvwProperties:Items[0]:SubItems:Add("").
		lvwProperties:Items[1]:SubItems:Add("").
		lvwProperties:Items[2]:SubItems:Add("").
		lvwProperties:Items[3]:SubItems:Add("").
		lvwProperties:Items[4]:SubItems:Add("").
		lvwProperties:Items[5]:SubItems:Add("").
		lvwProperties:Items[6]:SubItems:Add("").
		lvwProperties:Items[7]:SubItems:Add("").
		lvwProperties:Items[8]:SubItems:Add("").
		lvwProperties:Items[9]:SubItems:Add("").
		lvwProperties:Items[10]:SubItems:Add("").
		lvwProperties:Items[11]:SubItems:Add("").
		lvwProperties:Items[12]:SubItems:Add("").
		lvwProperties:Items[13]:SubItems:Add("").
		lvwProperties:Items[14]:SubItems:Add("").

		PositionGrid().		
 
		RETURN.

	END METHOD.

	/*------------------------------------------------------------------------------
			Purpose:  																	  
			Notes:  																	  
	------------------------------------------------------------------------------*/
	@VisualDesigner.
	METHOD PRIVATE VOID gridUIElements_InitializeLayout( INPUT sender AS System.Object, INPUT e AS Infragistics.Win.UltraWinGrid.InitializeLayoutEventArgs ):
		/*  Hide some key and parent columns */
		e:Layout:Bands[1]:Columns["CustNum"]:Hidden = true.

		e:Layout:Bands[2]:Columns["OrderNum"]:Hidden = true.
		
		e:Layout:Bands[2]:Columns["LineNum"]:Hidden = true.
		
		/*   Set the Format property of the Price columns  */
 		e:Layout:Bands[2]:Columns["Price"]:Format = "#,###,###.00".         	
		e:Layout:Bands[2]:Columns["ExtendedPrice"]:Format = "#,###,###.00".	
		
		/* right align price columns */
		e:Layout:Bands[2]:Columns["Price"]:CellAppearance:TextHAlign = Infragistics.Win.HAlign:Right.
		e:Layout:Bands[2]:Columns["ExtendedPrice"]:CellAppearance:TextHAlign = Infragistics.Win.HAlign:Right.
		
		e:Layout:Override:RowAppearance:AlphaLevel = ALPHA_LEVEL_ROW.
		e:Layout:Override:RowAppearance:BackColorAlpha = Infragistics.Win.Alpha:UseAlphaLevel.

		/* use the same appearance for alternate rows and the autopreview area */
		e:Layout:Override:RowAlternateAppearance = e:Layout:Override:RowAppearance.
		e:Layout:Override:RowPreviewAppearance:BackColor = System.Drawing.SystemColors:Window.
		e:Layout:Override:RowPreviewAppearance:ForeColor = System.Drawing.SystemColors:WindowText.

		e:Layout:Override:CellAppearance:BackColorAlpha = Infragistics.Win.Alpha:UseAlphaLevel.
		e:Layout:Override:CellAppearance:AlphaLevel = ALPHA_LEVEL_CELL.

		e:Layout:Override:HeaderAppearance:AlphaLevel = frmUIElements:ALPHA_LEVEL_HEADER.
		e:Layout:Override:HeaderAppearance:BackColorAlpha = Infragistics.Win.Alpha:UseAlphaLevel.

		e:Layout:Override:CellPadding = 4.
		e:Layout:Override:CellSpacing = 4.
		e:Layout:Override:CellClickAction = Infragistics.Win.UltraWinGrid.CellClickAction:RowSelect.
		e:Layout:Override:RowSpacingAfter = 8.

		gridUIElements:DisplayLayout:RowConnectorStyle = Infragistics.Win.UltraWinGrid.RowConnectorStyle:Raised.

		gridUIElements:DisplayLayout:AddNewBox:Appearance:BackColor = System.Drawing.Color:White.
		gridUIElements:DisplayLayout:AddNewBox:Appearance:AlphaLevel = ALPHA_LEVEL_ADDNEW.

		/* show name in the preview area of the first band  */
		e:Layout:Bands[0]:AutoPreviewField = "Name".
		e:Layout:Bands[0]:AutoPreviewEnabled = true.
		RETURN.

	END METHOD.

	/*------------------------------------------------------------------------------
			Purpose:  																	  
			Notes:  																	  
	------------------------------------------------------------------------------*/
	@VisualDesigner.
	METHOD PRIVATE VOID btnTitle_MouseUp( INPUT sender AS System.Object, INPUT e AS System.Windows.Forms.MouseEventArgs ):
		if valid-object(this-object:Owner) then
		    (cast (this-object:Owner,frmMain)):Navigation(e:Button).
		RETURN.

	END METHOD.

	/*------------------------------------------------------------------------------
			Purpose:  																	  
			Notes:  																	  
	------------------------------------------------------------------------------*/
	@VisualDesigner.
	METHOD PRIVATE VOID frmUIElements_Resize( INPUT sender AS System.Object, INPUT e AS System.EventArgs ):
		if this-object:WindowState = System.Windows.Forms.FormWindowState:Minimized then
			return.

	    PositionGrid().	
		RETURN.

	END METHOD.

	/*------------------------------------------------------------------------------
			Purpose:  																	  
			Notes:  																	  
	------------------------------------------------------------------------------*/
	@VisualDesigner.
	METHOD PRIVATE VOID gridUIElements_MouseLeaveElement( INPUT sender AS System.Object, INPUT e AS Infragistics.Win.UIElementEventArgs ):
		UnHighlightUIElement(e:Element).
		RETURN.

	END METHOD.

	/*------------------------------------------------------------------------------
			Purpose:  																	  
			Notes:  																	  
	------------------------------------------------------------------------------*/
	@VisualDesigner.
	METHOD PRIVATE VOID gridUIElements_MouseEnterElement( INPUT sender AS System.Object, INPUT e AS Infragistics.Win.UIElementEventArgs ):
 		DisplayUIElementProperties(e:Element).  
        DisplayUIElementAncestors(e:Element).
	    HighlightUIElement(e:Element).
		RETURN.

	END METHOD.
	/*------------------------------------------------------------------------------
			Purpose:  																	  
			Notes:  																	  
	------------------------------------------------------------------------------*/
	@VisualDesigner.
	METHOD PRIVATE VOID frmUIElements_FormClosing( INPUT sender AS System.Object, INPUT e AS System.Windows.Forms.FormClosingEventArgs ):
		if valid-object(this-object:Owner) then 
	    do:
	        e:Cancel = true.
		    this-object:Visible = false.
		end.
		RETURN.

	END METHOD.

END CLASS.