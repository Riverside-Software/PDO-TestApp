 /*------------------------------------------------------------------------
    File        : UltraTreeDatabindingForm
    Purpose     : 
    Syntax      : 
    Description : 
    Author(s)   : mferrant
    Created     : Wed May 28 16:23:32 EDT 2008
    Notes       : 
  ----------------------------------------------------------------------*/

USING Progress.Lang.*.
USING Progress.Windows.Form.

USING Infragistics.Win.UltraWinTree.*.
USING Infragistics.Win.UltraWinEditors.*.

using DataServices.*.

using WinTree.UltraTreeDataBinding.*.

CLASS WinTree.UltraTreeDataBinding.UltraTreeDatabindingForm INHERITS Form   : 
	DEFINE PRIVATE VARIABLE components AS System.ComponentModel.IContainer.

	DEFINE PRIVATE VARIABLE chkAutoFitColumns AS Infragistics.Win.UltraWinEditors.UltraCheckEditor NO-UNDO.
	DEFINE PRIVATE VARIABLE optBindTo AS Infragistics.Win.UltraWinEditors.UltraOptionSet NO-UNDO.
	DEFINE PRIVATE VARIABLE chkAlwaysShowBandNodes AS Infragistics.Win.UltraWinEditors.UltraCheckEditor NO-UNDO.
	DEFINE PRIVATE VARIABLE splitter1 AS System.Windows.Forms.Splitter NO-UNDO.
	DEFINE PRIVATE VARIABLE ultraGroupBox1 AS Infragistics.Win.Misc.UltraGroupBox NO-UNDO.
	DEFINE PRIVATE VARIABLE uddProducts AS Infragistics.Win.UltraWinGrid.UltraDropDown NO-UNDO.
	DEFINE PRIVATE VARIABLE optViewStyle AS Infragistics.Win.UltraWinEditors.UltraOptionSet NO-UNDO.
	DEFINE PRIVATE VARIABLE ultraTree1 AS Infragistics.Win.UltraWinTree.UltraTree NO-UNDO.
	DEFINE PRIVATE VARIABLE ultraGroupBox2 AS Infragistics.Win.Misc.UltraGroupBox NO-UNDO.
	DEFINE PRIVATE VARIABLE panel1 AS System.Windows.Forms.Panel NO-UNDO.

	DEFINE PRIVATE VARIABLE bindingSource1 AS Progress.Data.BindingSource NO-UNDO.

    DEFINE PRIVATE VARIABLE oCustOrderItemService AS DataServices.CustOrderItemService NO-UNDO.
    DEFINE PRIVATE VARIABLE oItemService AS  DataServices.ItemService NO-UNDO.

    
	CONSTRUCTOR PUBLIC UltraTreeDatabindingForm():
		SUPER().
		InitializeComponent().
			    InitializeUI().
		InitializeProBindingSourceObjects().

	END CONSTRUCTOR.

	DESTRUCTOR PUBLIC UltraTreeDatabindingForm ( ):
        IF VALID-OBJECT(oCustOrderItemService) THEN
            DELETE OBJECT oCustOrderItemService.

		IF VALID-OBJECT(components) THEN DO:
			CAST(components, System.IDisposable):Dispose().
		END.
	END DESTRUCTOR.

	METHOD PRIVATE VOID InitializeComponent ( ):

/* 		NOTE: The following method is generated by the OpenEdge Advanced GUI Visual Designer.

		We strongly suggest that the contents of this method only be modified using the
		Visual Designer to avoid any incompatible modifications.

		Modifying the contents of this method using a code editor will invalidate any support for this file. */

		@VisualDesigner.FormMember (NeedsInitialize="true").
		define variable appearance1 as Infragistics.Win.Appearance no-undo.
		appearance1 = new Infragistics.Win.Appearance().
		@VisualDesigner.FormMember (NeedsInitialize="true").
		define variable valueListItem1 as Infragistics.Win.ValueListItem no-undo.
		valueListItem1 = new Infragistics.Win.ValueListItem().
		@VisualDesigner.FormMember (NeedsInitialize="true").
		define variable valueListItem2 as Infragistics.Win.ValueListItem no-undo.
		valueListItem2 = new Infragistics.Win.ValueListItem().
		@VisualDesigner.FormMember (NeedsInitialize="true").
		define variable tableDesc1 as Progress.Data.TableDesc no-undo.
		tableDesc1 = new Progress.Data.TableDesc("eCustomer").
		@VisualDesigner.FormMember (NeedsInitialize="true").
		define variable tableDesc2 as Progress.Data.TableDesc no-undo.
		tableDesc2 = new Progress.Data.TableDesc("eOrder").
		@VisualDesigner.FormMember (NeedsInitialize="true").
		define variable tableDesc3 as Progress.Data.TableDesc no-undo.
		tableDesc3 = new Progress.Data.TableDesc("eOrderLine").
		this-object:chkAutoFitColumns = new Infragistics.Win.UltraWinEditors.UltraCheckEditor().
		this-object:optBindTo = new Infragistics.Win.UltraWinEditors.UltraOptionSet().
		this-object:chkAlwaysShowBandNodes = new Infragistics.Win.UltraWinEditors.UltraCheckEditor().
		this-object:splitter1 = new System.Windows.Forms.Splitter().
		this-object:ultraGroupBox1 = new Infragistics.Win.Misc.UltraGroupBox().
		this-object:uddProducts = new Infragistics.Win.UltraWinGrid.UltraDropDown().
		this-object:optViewStyle = new Infragistics.Win.UltraWinEditors.UltraOptionSet().
		this-object:ultraTree1 = new Infragistics.Win.UltraWinTree.UltraTree().
		this-object:bindingSource1 = new Progress.Data.BindingSource().
		this-object:ultraGroupBox2 = new Infragistics.Win.Misc.UltraGroupBox().
		this-object:panel1 = new System.Windows.Forms.Panel().
		cast(this-object:optBindTo, System.ComponentModel.ISupportInitialize):BeginInit().
		cast(this-object:ultraGroupBox1, System.ComponentModel.ISupportInitialize):BeginInit().
		this-object:ultraGroupBox1:SuspendLayout().
		cast(this-object:uddProducts, System.ComponentModel.ISupportInitialize):BeginInit().
		cast(this-object:optViewStyle, System.ComponentModel.ISupportInitialize):BeginInit().
		cast(this-object:ultraTree1, System.ComponentModel.ISupportInitialize):BeginInit().
		cast(this-object:bindingSource1, System.ComponentModel.ISupportInitialize):BeginInit().
		cast(this-object:ultraGroupBox2, System.ComponentModel.ISupportInitialize):BeginInit().
		this-object:ultraGroupBox2:SuspendLayout().
		this-object:panel1:SuspendLayout().
		this-object:SuspendLayout().
		/*  */
		/* chkAutoFitColumns */
		/*  */
		this-object:chkAutoFitColumns:Location = new System.Drawing.Point(16, 168).
		this-object:chkAutoFitColumns:Name = "chkAutoFitColumns".
		this-object:chkAutoFitColumns:Size = new System.Drawing.Size(192, 24).
		this-object:chkAutoFitColumns:TabIndex = 2.
		this-object:chkAutoFitColumns:Text = "Auto-Fit Columns".
		this-object:chkAutoFitColumns:CheckedChanged:subscribe(this-object:chkAutoFitColumns_CheckedChanged).
		/*  */
		/* optBindTo */
		/*  */
		this-object:optBindTo:BorderStyle = Infragistics.Win.UIElementBorderStyle:None.
		this-object:optBindTo:CheckedIndex = 0.
		appearance1:TextHAlignAsString = "Left".
		this-object:optBindTo:ItemAppearance = appearance1.
		valueListItem1:DataValue = "eCustomer".
		valueListItem1:DisplayText = "Customers".
		valueListItem2:DataValue = "eOrder".
		valueListItem2:DisplayText = "Order".
		@VisualDesigner.FormMember (NeedsInitialize="false", InitializeArray="true").
		define variable arrayvar0 as Infragistics.Win.ValueListItem extent 2 no-undo.
		arrayvar0[1] = valueListItem1.
		arrayvar0[2] = valueListItem2.
		this-object:optBindTo:Items:AddRange(arrayvar0).
		this-object:optBindTo:ItemSpacingVertical = 4.
		this-object:optBindTo:Location = new System.Drawing.Point(8, 19).
		this-object:optBindTo:Name = "optBindTo".
		this-object:optBindTo:Size = new System.Drawing.Size(184, 37).
		this-object:optBindTo:TabIndex = 0.
		this-object:optBindTo:Text = "Customers".
		this-object:optBindTo:ValueChanged:subscribe(this-object:optBindTo_ValueChanged).
		/*  */
		/* chkAlwaysShowBandNodes */
		/*  */
		this-object:chkAlwaysShowBandNodes:Location = new System.Drawing.Point(16, 136).
		this-object:chkAlwaysShowBandNodes:Name = "chkAlwaysShowBandNodes".
		this-object:chkAlwaysShowBandNodes:Size = new System.Drawing.Size(192, 24).
		this-object:chkAlwaysShowBandNodes:TabIndex = 1.
		this-object:chkAlwaysShowBandNodes:Text = "Always Show Band Nodes".
		this-object:chkAlwaysShowBandNodes:CheckedChanged:subscribe(this-object:chkAlwaysShowBandNodes_CheckedChanged).
		/*  */
		/* splitter1 */
		/*  */
		this-object:splitter1:Location = new System.Drawing.Point(216, 0).
		this-object:splitter1:Name = "splitter1".
		this-object:splitter1:Size = new System.Drawing.Size(3, 352).
		this-object:splitter1:TabIndex = 6.
		this-object:splitter1:TabStop = false.
		/*  */
		/* ultraGroupBox1 */
		/*  */
		this-object:ultraGroupBox1:Anchor = cast(Progress.Util.EnumHelper:Or(Progress.Util.EnumHelper:Or(System.Windows.Forms.AnchorStyles:Top, System.Windows.Forms.AnchorStyles:Left), System.Windows.Forms.AnchorStyles:Right), System.Windows.Forms.AnchorStyles).
		this-object:ultraGroupBox1:Controls:Add(this-object:optBindTo).
		this-object:ultraGroupBox1:Location = new System.Drawing.Point(8, 8).
		this-object:ultraGroupBox1:Name = "ultraGroupBox1".
		this-object:ultraGroupBox1:Size = new System.Drawing.Size(200, 67).
		this-object:ultraGroupBox1:TabIndex = 0.
		this-object:ultraGroupBox1:Text = "Bind to...".
		/*  */
		/* uddProducts */
		/*  */
		this-object:uddProducts:Location = new System.Drawing.Point(447, 263).
		this-object:uddProducts:Name = "uddProducts".
		this-object:uddProducts:Size = new System.Drawing.Size(265, 64).
		this-object:uddProducts:TabIndex = 7.
		this-object:uddProducts:Text = "ultraDropDown1".
		this-object:uddProducts:Visible = false.
		/*  */
		/* optViewStyle */
		/*  */
		this-object:optViewStyle:BorderStyle = Infragistics.Win.UIElementBorderStyle:None.
		this-object:optViewStyle:Location = new System.Drawing.Point(8, 24).
		this-object:optViewStyle:Name = "optViewStyle".
		this-object:optViewStyle:Size = new System.Drawing.Size(184, 80).
		this-object:optViewStyle:TabIndex = 0.
		this-object:optViewStyle:ValueChanged:subscribe(this-object:optViewStyle_ValueChanged).
		/*  */
		/* ultraTree1 */
		/*  */
		this-object:ultraTree1:Dock = System.Windows.Forms.DockStyle:Fill.
		this-object:ultraTree1:Location = new System.Drawing.Point(216, 0).
		this-object:ultraTree1:Name = "ultraTree1".
		this-object:ultraTree1:Size = new System.Drawing.Size(519, 352).
		this-object:ultraTree1:TabIndex = 4.
		this-object:ultraTree1:AfterDataNodesCollectionPopulated:subscribe(this-object:ultraTree1_AfterDataNodesCollectionPopulated).
		this-object:ultraTree1:ColumnSetGenerated:subscribe(this-object:ultraTree1_ColumnSetGenerated).
		this-object:ultraTree1:InitializeDataNode:subscribe(this-object:ultraTree1_InitializeDataNode).
		/*  */
		/* bindingSource1 */
		/*  */
		this-object:bindingSource1:AllowEdit = false.
		this-object:bindingSource1:AllowNew = false.
		this-object:bindingSource1:AllowRemove = false.
		this-object:bindingSource1:Position = 0.
		tableDesc3:ChildTables = ?.
		@VisualDesigner.FormMember (NeedsInitialize="false", InitializeArray="true").
		define variable arrayvar1 as Progress.Data.ColumnPropDesc extent 6 no-undo.
		arrayvar1[1] = new Progress.Data.ColumnPropDesc("Ordernum", "Order Num", Progress.Data.DataType:INTEGER).
		arrayvar1[2] = new Progress.Data.ColumnPropDesc("Linenum", "Line Num", Progress.Data.DataType:INTEGER).
		arrayvar1[3] = new Progress.Data.ColumnPropDesc("Itemnum", "Item Num", Progress.Data.DataType:INTEGER).
		arrayvar1[4] = new Progress.Data.ColumnPropDesc("Price", "Price", Progress.Data.DataType:DECIMAL).
		arrayvar1[5] = new Progress.Data.ColumnPropDesc("Qty", "Qty", Progress.Data.DataType:INTEGER).
		arrayvar1[6] = new Progress.Data.ColumnPropDesc("Discount", "Discount", Progress.Data.DataType:INTEGER).
		tableDesc3:Columns = arrayvar1.
		@VisualDesigner.FormMember (NeedsInitialize="false", InitializeArray="true").
		define variable arrayvar2 as Progress.Data.TableDesc extent 1 no-undo.
		arrayvar2[1] = tableDesc3.
		tableDesc2:ChildTables = arrayvar2.
		@VisualDesigner.FormMember (NeedsInitialize="false", InitializeArray="true").
		define variable arrayvar3 as Progress.Data.ColumnPropDesc extent 6 no-undo.
		arrayvar3[1] = new Progress.Data.ColumnPropDesc("Ordernum", "Order Num", Progress.Data.DataType:INTEGER).
		arrayvar3[2] = new Progress.Data.ColumnPropDesc("CustNum", "Cust Num", Progress.Data.DataType:INTEGER).
		arrayvar3[3] = new Progress.Data.ColumnPropDesc("OrderDate", "Ordered", Progress.Data.DataType:DATE).
		arrayvar3[4] = new Progress.Data.ColumnPropDesc("ShipDate", "Shipped", Progress.Data.DataType:DATE).
		arrayvar3[5] = new Progress.Data.ColumnPropDesc("PromiseDate", "Promised", Progress.Data.DataType:DATE).
		arrayvar3[6] = new Progress.Data.ColumnPropDesc("OrderStatus", "Order Status", Progress.Data.DataType:CHARACTER).
		tableDesc2:Columns = arrayvar3.
		@VisualDesigner.FormMember (NeedsInitialize="false", InitializeArray="true").
		define variable arrayvar4 as Progress.Data.TableDesc extent 1 no-undo.
		arrayvar4[1] = tableDesc2.
		tableDesc1:ChildTables = arrayvar4.
		@VisualDesigner.FormMember (NeedsInitialize="false", InitializeArray="true").
		define variable arrayvar5 as Progress.Data.ColumnPropDesc extent 5 no-undo.
		arrayvar5[1] = new Progress.Data.ColumnPropDesc("Name", "Name", Progress.Data.DataType:CHARACTER).
		arrayvar5[2] = new Progress.Data.ColumnPropDesc("CustNum", "CustNum", Progress.Data.DataType:INTEGER).
		arrayvar5[3] = new Progress.Data.ColumnPropDesc("Phone", "Phone", Progress.Data.DataType:CHARACTER).
		arrayvar5[4] = new Progress.Data.ColumnPropDesc("Contact", "Contact", Progress.Data.DataType:CHARACTER).
		arrayvar5[5] = new Progress.Data.ColumnPropDesc("Balance", "Balance", Progress.Data.DataType:DECIMAL).
		tableDesc1:Columns = arrayvar5.
		this-object:bindingSource1:TableSchema = tableDesc1.
		/*  */
		/* ultraGroupBox2 */
		/*  */
		this-object:ultraGroupBox2:Anchor = cast(Progress.Util.EnumHelper:Or(Progress.Util.EnumHelper:Or(System.Windows.Forms.AnchorStyles:Top, System.Windows.Forms.AnchorStyles:Left), System.Windows.Forms.AnchorStyles:Right), System.Windows.Forms.AnchorStyles).
		this-object:ultraGroupBox2:Controls:Add(this-object:optViewStyle).
		this-object:ultraGroupBox2:Location = new System.Drawing.Point(8, 200).
		this-object:ultraGroupBox2:Name = "ultraGroupBox2".
		this-object:ultraGroupBox2:Size = new System.Drawing.Size(200, 112).
		this-object:ultraGroupBox2:TabIndex = 3.
		this-object:ultraGroupBox2:Text = "View Style".
		/*  */
		/* panel1 */
		/*  */
		this-object:panel1:Controls:Add(this-object:ultraGroupBox2).
		this-object:panel1:Controls:Add(this-object:chkAutoFitColumns).
		this-object:panel1:Controls:Add(this-object:chkAlwaysShowBandNodes).
		this-object:panel1:Controls:Add(this-object:ultraGroupBox1).
		this-object:panel1:Dock = System.Windows.Forms.DockStyle:Left.
		this-object:panel1:Location = new System.Drawing.Point(0, 0).
		this-object:panel1:Name = "panel1".
		this-object:panel1:Size = new System.Drawing.Size(216, 352).
		this-object:panel1:TabIndex = 5.
		/*  */
		/* UltraTreeDatabindingForm */
		/*  */
		this-object:ClientSize = new System.Drawing.Size(735, 352).
		this-object:Controls:Add(this-object:splitter1).
		this-object:Controls:Add(this-object:uddProducts).
		this-object:Controls:Add(this-object:ultraTree1).
		this-object:Controls:Add(this-object:panel1).
		this-object:Name = "UltraTreeDatabindingForm".
		this-object:Text = "UltraTreeDatabindingForm".
		cast(this-object:optBindTo, System.ComponentModel.ISupportInitialize):EndInit().
		cast(this-object:ultraGroupBox1, System.ComponentModel.ISupportInitialize):EndInit().
		this-object:ultraGroupBox1:ResumeLayout(false).
		cast(this-object:uddProducts, System.ComponentModel.ISupportInitialize):EndInit().
		cast(this-object:optViewStyle, System.ComponentModel.ISupportInitialize):EndInit().
		cast(this-object:ultraTree1, System.ComponentModel.ISupportInitialize):EndInit().
		cast(this-object:bindingSource1, System.ComponentModel.ISupportInitialize):EndInit().
		cast(this-object:ultraGroupBox2, System.ComponentModel.ISupportInitialize):EndInit().
		this-object:ultraGroupBox2:ResumeLayout(false).
		this-object:panel1:ResumeLayout(false).
		this-object:ResumeLayout(false).
	END METHOD.

	/*------------------------------------------------------------------------------
			Purpose:
			Notes:
	------------------------------------------------------------------------------*/
	@VisualDesigner.
	METHOD PRIVATE VOID optBindTo_ValueChanged( INPUT sender AS System.Object, INPUT e AS System.EventArgs ):
	    DEFINE VARIABLE bindTo AS CHARACTER NO-UNDO.

	    ASSIGN bindTo = optBindTo:VALUE:ToString().
	    CASE bindTo:
	        WHEN "eCustomer" THEN
                ultraTree1:SetDataBinding(bindingSource1, "").
            WHEN "eOrder" THEN
                ultraTree1:SetDataBinding(bindingSource1,  optBindTo:VALUE:ToString() ).
        END CASE.
	END METHOD.

	/*------------------------------------------------------------------------------
			Purpose:
			Notes:
	------------------------------------------------------------------------------*/
	METHOD PRIVATE VOID InitializeUI(  ):
	    DEFINE VARIABLE viewStyles   AS System.Array NO-UNDO.
	    DEFINE VARIABLE totalStyles  AS INTEGER      NO-UNDO.
	    DEFINE VARIABLE currentStyle AS INTEGER      NO-UNDO.
	    DEFINE VARIABLE style        AS ViewStyle    NO-UNDO.

        DEFINE VARIABLE bigMoneyAppearance AS Infragistics.Win.Appearance NO-UNDO.

        /*Fills the style combo-box with all the possible UltraTree styles.*/
	    ASSIGN viewStyles  = System.Enum:GetValues(Progress.Util.TypeHelper:GetType("Infragistics.Win.UltraWinTree.ViewStyle")).
	           totalStyles = viewStyles:LENGTH - 1.

	    REPEAT currentStyle = 1 TO totalStyles:
	        ASSIGN style = CAST(viewStyles:GetValue(currentStyle), ViewStyle).
		    optViewStyle:Items:Add(style, style:ToString()).
	    END.
        optViewStyle:CheckedIndex = 0.
        
    	/* Bind uddProducts (UltraDropDown control) to Items dataset.  */
		/* This will be used as a ValueList in the Invoice Details table to  */
		/* translate ItemNum into more user-friendly product names. */
		
	    uddProducts:ValueMember = "ItemNum".
		uddProducts:DisplayMember = "ItemName".
			
	    /*Create an Appearance in the tree that will be used to highlight
		  customer balances over $1000.*/ 
		ASSIGN
		bigMoneyAppearance                   = ultraTree1:Appearances:Add("BigMoney")	
		bigMoneyAppearance:BackColor         = System.Drawing.Color:FromArgb(244, 250, 222)
	    bigMoneyAppearance:BackColor2        = System.Drawing.Color:FromArgb(107, 121, 86)			
		bigMoneyAppearance:BackGradientStyle = Infragistics.Win.GradientStyle:Vertical
		bigMoneyAppearance:TextVAlign        = Infragistics.Win.VAlign:Middle.			

		/*Set the ShowExpansionIndicator property to 'CheckOnDisplay' so we don't display
		  an expansion indicator for nodes that have no descendants.*/
		ultraTree1:OVERRIDE:ShowExpansionIndicator = ShowExpansionIndicator:CheckOnDisplay.
	END METHOD.

	/*------------------------------------------------------------------------------
			Purpose:
			Notes:
	------------------------------------------------------------------------------*/
	@VisualDesigner.
	METHOD PRIVATE VOID chkAlwaysShowBandNodes_CheckedChanged( INPUT sender AS System.Object, INPUT e AS System.EventArgs ):
	    DEFINE VARIABLE ultraCheckEditor AS UltraCheckEditor NO-UNDO.

	    ultraCheckEditor = CAST(sender, UltraCheckEditor).

	    IF ultraCheckEditor:CHECKED THEN
	        ultraTree1:ColumnSettings:ShowBandNodes = ShowBandNodes:Always.
		ELSE
		    ultraTree1:ColumnSettings:ShowBandNodes = ShowBandNodes:OnlyForSiblingBands.
	END METHOD.

	/*------------------------------------------------------------------------------
			Purpose:
			Notes:
	------------------------------------------------------------------------------*/
	METHOD PRIVATE VOID InitializeProBindingSourceObjects(  ):
	    define variable itemBindingSource as Progress.Data.BindingSource no-undo.
	    IF VALID-OBJECT(oCustOrderItemService) THEN DELETE OBJECT oCustOrderItemService.
        IF VALID-OBJECT(oItemService) THEN DELETE OBJECT oItemService.

		oCustOrderItemService = NEW CustOrderItemService().
		oCustOrderItemService:fetchData().
		
		bindingSource1:HANDLE = oCustOrderItemService:DatasetHandle.
		ultraTree1:SetDataBinding(bindingSource1, "").
		itemBindingSource = new Progress.Data.BindingSource().
		itemBindingSource:Handle = oCustOrderItemService:OpenQuery("eItem").
		uddProducts:SetDataBinding(itemBindingSource, "").
    	
	END METHOD.

	/*------------------------------------------------------------------------------
			Purpose:
			Notes:
	------------------------------------------------------------------------------*/
	@VisualDesigner.
	METHOD PRIVATE VOID optViewStyle_ValueChanged( INPUT sender AS System.Object, INPUT e AS System.EventArgs ):
	    DEFINE VARIABLE ultraOptionSet AS UltraOptionSet NO-UNDO.
	    DEFINE VARIABLE lMessage       AS LOGICAL NO-UNDO.

	    ultraOptionSet = CAST(sender,UltraOptionSet).
		ultraTree1:ViewStyle = CAST(ultraOptionSet:CheckedItem:DataValue, ViewStyle).

		/*AutoFitColumns only works when ViewStyle is OutlookExpress*/
		IF chkAutoFitColumns:Checked AND ultraTree1:ViewStyle NE ViewStyle:OutlookExpress THEN
        DO:
		    MESSAGE "AutoFitColumns is only supported for OutlookExpress ViewStyle. Would you like to turn off AutoFitColumns now?"
		    VIEW-AS ALERT-BOX QUESTION
            BUTTONS YES-NO
		    TITLE "Turn off AutoFitColumns"
		    UPDATE lMessage.

		    IF lMessage THEN
			    chkAutoFitColumns:Checked = FALSE.
		END.

	END METHOD.

	/*------------------------------------------------------------------------------
			Purpose:
			Notes:
	------------------------------------------------------------------------------*/
	@VisualDesigner.
	METHOD PRIVATE VOID chkAutoFitColumns_CheckedChanged( INPUT sender AS System.Object, INPUT e AS System.EventArgs ):
	    DEFINE VARIABLE ultraCheckEditor AS UltraCheckEditor NO-UNDO.
	    DEFINE VARIABLE lMessage         AS LOGICAL          NO-UNDO.

        ultraCheckEditor = CAST(sender, UltraCheckEditor).

	    IF ultraCheckEditor:CHECKED THEN
		    ultraTree1:ColumnSettings:AutoFitColumns = AutoFitColumns:ResizeAllColumns.
		ELSE
		    ultraTree1:ColumnSettings:AutoFitColumns = AutoFitColumns:None.

		/*AutoFitColumns only works when ViewStyle is OutlookExpress*/
		IF ultraTree1:ColumnSettings:AutoFitColumns = AutoFitColumns:ResizeAllColumns AND
		   Progress.Util.EnumHelper:AreNotEqual(ultraTree1:ViewStyle,ViewStyle:OutlookExpress) THEN
        DO:
			MESSAGE "AutoFitColumns is only supported for OutlookExpress ViewStyle. Would you like to switch to OutlookExpress ViewStyle now?"
		    VIEW-AS ALERT-BOX QUESTION
            BUTTONS YES-NO
		    TITLE "Change ViewStyle?"
		    UPDATE lMessage.

            IF lMessage THEN
		        optViewStyle:Value = ViewStyle:OutlookExpress.
	    END.
	END METHOD.

	/*------------------------------------------------------------------------------
			Purpose:  																	  
			Notes:  																	  
	------------------------------------------------------------------------------*/
	@VisualDesigner.
	METHOD PRIVATE VOID ultraTree1_InitializeDataNode( INPUT sender AS System.Object, INPUT e AS Infragistics.Win.UltraWinTree.InitializeDataNodeEventArgs ):
	    DEFINE VARIABLE cellValue AS DECIMAL NO-UNDO.
	    /*This event fires every time a node is added to the tree from a data source. 
		  It's a good place to apply Appearances that are dependent on the value of a cell. 

		  The following code will highlight any customer.balance over $1000 
		  by applying the "bigMoney" Appearance which was creates in earlier.*/
   
		  /*Now check to see if this is the correct band - Customer = "".*/
		  CASE e:Node:BandName:
       	      
		  WHEN "" THEN
          DO:
             /*Get the value of the Balance cell*/                              
 			 cellValue = UNBOX(e:Node:Cells["Balance"]:Value).                            

		     IF cellValue >= 1000 THEN					
			     /*If the value is more than 100., apply the "BigMoney" appearance.*/
			     e:Node:Cells["Balance"]:Appearance = ultraTree1:Appearances["BigMoney"].
			 ELSE
			     /*Otherwise, reset the Appearance of the cell.
					This is neccessary in case the underlying value of the
					cell in the DataSet changes. In that case, this event
					will fire again - which gives us the opportunity to clear the Appearance.*/
			      e:Node:Cells["Balance"]:ResetAppearance().
          END.
          WHEN "eOrder" THEN
          DO:
              e:Node:Text = UNBOX(e:Node:Cells["OrderDate"]:Value).
          END.
          END CASE.
	END METHOD.

	/*------------------------------------------------------------------------------
			Purpose:  																	  
			Notes:  																	  
	------------------------------------------------------------------------------*/
	@VisualDesigner.
	METHOD PRIVATE VOID ultraTree1_AfterDataNodesCollectionPopulated( INPUT sender AS System.Object, INPUT e AS Infragistics.Win.UltraWinTree.AfterDataNodesCollectionPopulatedEventArgs ):
	    DEFINE VARIABLE treeColumns   AS TreeColumnsCollection NO-UNDO.
	    DEFINE VARIABLE treeColumn    AS UltraTreeNodeColumn   NO-UNDO.
        DEFINE VARIABLE totalColumns  AS INTEGER               NO-UNDO.
        DEFINE VARIABLE currentColumn AS INTEGER               NO-UNDO.

        /*This event fires after the tree has populated a Nodes collection with data from the data source. 
		  Since the nodes collection has been populated with nodes at this point,
		  this is an excellent place to autosize the columns based on their contents.*/
		ASSIGN treeColumns = e:Nodes:ColumnSetResolved:Columns
		       totalColumns = treeColumns:COUNT - 1.

        REPEAT currentColumn = 0 TO totalColumns:
		    treeColumn = CAST(treeColumns:GetItem(currentColumn), UltraTreeNodeColumn).
			treeColumn:PerformAutoResize().
		END.
	END METHOD.

	/*------------------------------------------------------------------------------
			Purpose:  																	  
			Notes:  																	  
	------------------------------------------------------------------------------*/
	@VisualDesigner.
	METHOD PRIVATE VOID ultraTree1_ColumnSetGenerated( INPUT sender AS System.Object, INPUT e AS Infragistics.Win.UltraWinTree.ColumnSetGeneratedEventArgs ):
	    /*This event will fire whenever the tree automatically generates a ColumnSet.
		  The ColumnSet is not neccessarily attach to any particular group of
		  Nodes at this point, so this is an appropriate place to apply appearances 
		  and properties that apply at a ColumnSet level.*/ 

		/*The Key of the generated ColumnSet will match the key of the band of data.*/
		CASE e:ColumnSet:Key:
		    /*eCustomer*/
		    WHEN "" THEN
            DO:
				/*Highlight Name column*/
				ASSIGN 
				e:ColumnSet:Columns["Name"]:CellAppearance:BackColor            = System.Drawing.Color:FromArgb(218,235,255)
				e:ColumnSet:Columns["Name"]:CellAppearance:BackColor2           = System.Drawing.Color:FromArgb(129,168,225)
				e:ColumnSet:Columns["Name"]:CellAppearance:BackGradientStyle    = Infragistics.Win.GradientStyle:VERTICAL
				e:ColumnSet:Columns["Name"]:CellAppearance:TextVAlign           = Infragistics.Win.VAlign:Middle
				e:ColumnSet:Columns["CustNum"]:CellAppearance:BackColor         = System.Drawing.Color:FromArgb(218,255,255)
				e:ColumnSet:Columns["CustNum"]:CellAppearance:BackColor2        = System.Drawing.Color:FromArgb(129,168,225)
				e:ColumnSet:Columns["CustNum"]:CellAppearance:BackGradientStyle = Infragistics.Win.GradientStyle:VERTICAL
				e:ColumnSet:Columns["CustNum"]:CellAppearance:TextVAlign        = Infragistics.Win.VAlign:Middle.
			END.

			WHEN "eOrder" THEN
            DO:
			    /*Hide the OrderNum and CustNum columns.*/
			    ASSIGN e:ColumnSet:Columns["OrderNum"]:Visible = FALSE
					   e:ColumnSet:Columns["CustNum"]:Visible  = FALSE.
 
				/*Highlight OrderDate column.*/
				ASSIGN e:ColumnSet:Columns["OrderDate"]:CellAppearance:BackColor    = System.Drawing.Color:FromArgb(182,197,154)
				       e:ColumnSet:Columns["OrderDate"]:Format   = "d"
				       e:ColumnSet:Columns["ShipDate"]:Format    = "d"
				       e:ColumnSet:Columns["PromiseDate"]:Format = "d"
				       e:ColumnSet:Columns["OrderDate"]:CellAppearance:TextHAlign   = Infragistics.Win.HAlign:Right
				       e:ColumnSet:Columns["ShipDate"]:CellAppearance:TextHAlign    = Infragistics.Win.HAlign:Right
				       e:ColumnSet:Columns["PromiseDate"]:CellAppearance:TextHAlign = Infragistics.Win.HAlign:Right.
			END.

			WHEN "eOrderLine" THEN
            DO:
			    /*Hide the DetailID and InvoiceID columns.*/
				ASSIGN e:ColumnSet:Columns["OrderNum"]:Visible = FALSE
					   e:ColumnSet:Columns["LineNum"]:Visible  = FALSE.
				/*	   e:ColumnSet:Columns["ItemNum"]:Visible  = FALSE.*/

				/*Highlight Total column.*/
				e:ColumnSet:Columns["Price"]:CellAppearance:BackColor = System.Drawing.Color:FromArgb(182,197,154).

				/*Assign a ValueList to the ProductID column to translate
				  the ProductID into a more user-friendly product name.
				  Note that UltraTree cells are not editable. The ValueList
				  in this case is for the purpose of translating data values
				  into display values. It will not drop down.*/  
				e:ColumnSet:Columns["ItemNum"]:ValueList = uddProducts.

				/*Since the ProductID column will be showing names instead of IDs
				  change the caption.*/ 
				e:ColumnSet:Columns["ItemNum"]:Text = "Product Name".
			END.

		END CASE.			
	END METHOD.

	/*------------------------------------------------------------------------------
			Purpose:  																	  
			Notes:  																	  
	------------------------------------------------------------------------------*/
	@VisualDesigner.
	METHOD PRIVATE VOID UltraTreeDatabindingForm_Load( INPUT sender AS System.Object, INPUT e AS System.EventArgs ):
		ultraTree1:Nodes:Override:Sort = SortType:ASCENDING.
	END METHOD.

 END CLASS